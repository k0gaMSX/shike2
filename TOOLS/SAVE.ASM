
_CONOUT	EQU	02H
_STROUT	EQU	09H
_CREATE	EQU	44H
_CLOSE	EQU	45H
_WRITE	EQU	49H
EXPLAIN	EQU	66H
BDOS	EQU	05H


	CSEG
	PUBLIC	MAIN

MAIN:	LD	HL,DATA
LOOP:	LD	A,(HL)
	OR	A
	RET	Z

	OUT	(0FDH),A
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(PTR),HL

	LD	(FNAME),DE
	LD	C,_CREATE
	LD	B,0
	XOR	A
	CALL	BDOS
	OR	A
	JR	NZ,SYSERR

	LD	A,B
	LD	(FILE),A
	LD	HL,4000H
	LD	DE,4000H
	LD	C,_WRITE
	CALL	BDOS
	OR	A
	JR	NZ,SYSERR

	LD	A,(FILE)
	LD	B,A
	LD	C,_CLOSE
	CALL	BDOS
	OR	A
	JR	NZ,SYSERR
	LD	HL,(PTR)
	JR	LOOP

PUTS:	LD	A,(HL)
	OR	A
	RET	Z

	PUSH	HL
	LD	C,_CONOUT
	LD	E,A
	CALL	BDOS
	POP	HL
	INC	HL
	JR	PUTS

SYSERR:	PUSH	AF
	LD	HL,(FNAME)
	CALL	PUTS
	LD	E,':'
	LD	C,_CONOUT
	CALL	BDOS
	POP	BC
	LD	DE,ERRSTR
	LD	C,EXPLAIN
	CALL	BDOS

	LD	HL,ERRSTR
	LD	BC,64
	XOR	A
	CPIR
	INC	HL
	INC	HL
	LD	(HL),'$'
	DEC	HL
	LD	(HL),0AH
	DEC	HL
	LD	(HL),0DH
	LD	DE,ERRSTR
	LD	C,_STROUT
	JP	BDOS

DATA:	DB	7
	DW	LVLDEF
	DB	8
	DW	MAP0
	DB	9
	DW	MAP1
	DB	10
	DW	MAP2
	DB	11
	DW	MAP3
	DB	12
	DW	HEIGHT0
	DB	13
	DW	HEIGHT1
	DB	0

LVLDEF:	DB	"LEVELDEF.DAT",0
MAP0:	DB	"MAP0.DAT",0
MAP1:	DB	"MAP1.DAT",0
MAP2:	DB	"MAP2.DAT",0
MAP3:	DB	"MAP3.DAT",0
HEIGHT0:DB	"HEIGHT0.DAT",0
HEIGHT1:DB	"HEIGHT1.DAT",0


	DSEG
ERRSTR:	DS	64
FNAME:	DW	0
FILE:	DB	0
PTR:	DW	0


