

_CREATE		EQU	44H
_STROUT		EQU	09H
_OPEN		EQU	43H
_CREATE		EQU	44H
_CLOSE		EQU	45H
_READ		EQU	48H
_WRITE		EQU	49H
_EXPLAIN	EQU	66H

BDOS	EQU	5

.EOF	EQU	0C7H

ARG	EQU	80H
MAXARG	EQU	10H

	CSEG
	PUBLIC	MAIN

MAIN:	CALL	CRT0
	LD	A,-1
	LD	(FIN),A
	LD	(FOUT),A
	XOR	A
	LD	(ERR),A
	LD	DE,0
	LD	(STR),DE

	LD	A,(ARGC)
	CP	2
	JR	Z,M.1
	LD	DE,ERRPAR
	LD	(STR),DE
	JP	M.ERR

M.1:	LD	DE,(ARGV)
	LD	C,_OPEN
	XOR	A
	CALL	BDOS
	OR	A
	JR	Z,M.2
	LD	DE,ERROIN
	LD	(STR),DE
	LD	(ERR),A
	JP	M.ERR

M.2:	LD	A,B
	LD	(FIN),A
	LD	DE,(ARGV+2)
	LD	C,_CREATE
	LD	B,80H
	XOR	A
	CALL	BDOS
	OR	A
	JR	Z,M.3
	LD	DE,ERROIN
	LD	(STR),DE
	LD	(ERR),A
	JP	M.ERR1

M.3:	LD	A,B
	LD	(FOUT),A
	LD	A,(FIN)			;READ FIRST 4 BYTES
	LD	B,A
	LD	DE,BUFFER
	LD	HL,4
	LD	C,_READ
	CALL	BDOS
	OR	A
	JR	Z,M.LOOP
	LD	DE,ERRRIN
	LD	(ERR),A
	LD	(STR),DE
	JP	M.ERR2

M.LOOP:	LD	A,(FIN)			;READ THE FULL FILE
	LD	B,A
	LD	DE,BUFFER
	LD	HL,512
	LD	C,_READ
	CALL	BDOS
	OR	A
	JR	Z,M.4
	CP	.EOF			;END OF FILE? IT IS CORRECT
	JP	Z,M.ERR2
	LD	DE,ERRRIN
	LD	(STR),DE
	LD	(ERR),A
	JP	M.ERR2	

M.4:	LD	A,(FOUT)
	LD	B,A
	LD	DE,BUFFER
	LD	C,_WRITE
	CALL	BDOS
	OR	A
	JR	Z,M.LOOP
	LD	DE,ERRWOUT
	LD	(STR),DE
	LD	(ERR),A

;;;;;;;;;;;;;;;;;;;;;;;;;;;

M.ERR2:	LD	A,(FOUT)
	LD	B,A
	LD	C,_CLOSE
	CALL	BDOS

M.ERR1:	LD	A,(FIN)
	LD	B,A
	LD	C,_CLOSE
	CALL	BDOS

M.ERR:	LD	A,(ERR)
	OR	A
	JR	Z,M.END
	LD	C,_EXPLAIN
	LD	DE,ERRSTR
	CALL	BDOS
	LD	HL,ERRSTR
	LD	BC,64
	XOR	A
	CPIR
	INC	HL
	INC	HL
	LD	(HL),'$'
	DEC	HL
	LD	(HL),0AH
	DEC	HL	
	LD	(HL),0DH
	LD	DE,ERRSTR
	LD	C,_STROUT
	CALL	BDOS

M.END:	LD	DE,(STR)
	LD	A,D
	OR	E
	RET	Z
	LD	C,_STROUT
	JP	BDOS


ERRWOUT:DB	"Error writing output file",0ah,0dh,'$'
ERRRIN:	DB	"Error reading input file",0ah,0dh,'$'
ERROOUT:DB	"Error opening output file",0ah,0dh,'$'
ERROIN:	DB	"Error opening input file",0ah,0dh,'$'
ERRPAR:	DB	"Error in parameters",0ah,0dh,'$'


	DSEG
FIN:	DB	0
FOUT:	DB	0
STR:	DW	0
ERR:	DB	0
BUFFER:	DS	512
ERRSTR:	DS	64

	CSEG
	PUBLIC	CRT0

CRT0:	LD	HL,ARGV
	LD	C,0
	EXX
	LD	HL,ARG+1
	LD	DE,ARG+80H

C.LOOK:	LD	A,(HL)
	CP	' '
	JR	Z,C.NARG

	PUSH	HL
	EXX
	POP	DE
	LD	(HL),E
	INC	HL
	LD	(HL),D
	INC	HL
	INC	C
	LD	A,C
	LD	(ARGC),A
	EXX

C.EOW:	INC	HL
	LD	A,(HL)
	OR	A
	RET	Z
	CP	' '
	JR	Z,C.LOOK
	CALL	DCOMPR
	RET	Z
	JR	C.EOW

C.NARG:	LD	(HL),0
	INC	HL
	LD	A,(HL)
	OR	A
	RET	Z
	CALL	DCOMPR
	JR	NZ,C.LOOK
	RET

DCOMPR:	LD	A,H
	CP	D
	RET	NZ
	LD	A,L
	CP	E
	RET

	DSEG
	PUBLIC	ARGC,ARGV

ARGC:	DB	0
ARGV:	DS	MAXARG*2
