
	INCLUDE	SHIKE2.INC
	INCLUDE	EDITOR.INC


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	HL = POINTER TO THE OUTPUT BUFFER
;	DE = TILE POSITION OF LEFT-UP CORNER
;	BC = SIZE OF SQUARE
;	A = Z VALUE
;OUTPUT:HL = POINTER TO OUTPUT BUFFER AFTER CRUNCH

	CSEG
	EXTRN	PACK
	PUBLIC	H.CRUNCH

H.CRUNCH:
	LD	(HL),A
	INC	HL
	CALL	PACK
	LD	(HL),A
	INC	HL
	DEC	B			;VALUES ALLOWED = 0 UNTIL 15
	DEC	C
	LD	E,C
	LD	D,B
	CALL	PACK
	LD	(HL),A
	INC	HL
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	HL = POINTER TO THE OUTPUT BUFFER
;OUTPUT:HL = POINTER TO OUTPUT BUFFER AFTER CRUNCH
;	DE = TILE POSITION OF LEFT-UP CORNER
;	BC = SIZE OF SQUARE
;	A = Z VALUE

	CSEG
	PUBLIC	H.DECRUNCH
	EXTRN	UNPACK

H.DECRUNCH:
	LD	A,(HL)
	INC	HL
	PUSH	AF
	LD	A,(HL)
	INC	HL
	CALL	UNPACK
	PUSH	DE
	LD	A,(HL)
	INC	HL
	CALL	UNPACK
	LD	C,E
	LD	B,D
	POP	DE
	POP	AF
	INC	B			;VALUES ALLOWED = 1 UNTIL 16
	INC	C
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = LEFT-UP CORNER
;	HL = MATRIX POINTER
;OUTPUT:HL = ADDRESS OF THE BYTE WHICH CONSTAINS THE HEIGTH


	CSEG

ADDRSQR:PUSH	HL
	LD	L,E
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL		; HL = CORNERY * HEIGTHROWSIZ (YOFFSET)
	LD	E,D
	LD	D,0
	ADD	HL,DE		; HL = YOFFSET + XOFFSET
	POP	DE
	ADD	HL,DE		; HL = MATRIX + YOFFSET + XOFFSET
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = LEFT-UP CORNER
;	BC = SIZE OF SQUARE
;	HL = MATRIX POINTER
;	A = Z VALUE

	CSEG
	EXTRN	MEMSET

FILLSQR:LD	(F.ZVAL),A
	CALL	ADDRSQR

S.LOOP:	PUSH	BC
	PUSH	HL
	LD	A,(F.ZVAL)
	LD	C,B
	LD	B,0
	CALL	MEMSET
	POP	HL
	LD	DE,HEIGTHROWSIZ
	ADD	HL,DE			;HL = POINT TO NEXT ROW
	POP	BC
	DEC	C
	JR	NZ,S.LOOP
	RET

	DSEG
F.ZVAL:	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = INPUT HEIGTH BUFFER
;	BC = OUTPUT HEIGTH MATRIX

	CSEG
	PUBLIC	HMATRIX
	EXTRN	BZERO

HMATRIX:LD	(H.MATRIX),BC
	PUSH	DE
	LD	L,C
	LD	H,B
	LD	BC,HEIGTHMATRIXSIZ
	CALL	BZERO

	POP	HL
	LD	A,(HL)
	OR	A
	RET	Z
	INC	HL
        LD	B,A

M.LOOP:	PUSH	BC
	CALL	H.DECRUNCH		;DECRUNCH EACH COMMAND
	PUSH	HL
	LD	HL,(H.MATRIX)
	CALL	FILLSQR			;FILL SQUARE
	POP	HL
	POP	BC
	DJNZ	M.LOOP
	RET

	DSEG
H.MATRIX:	DW	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = TILE POSITION
;	BC = MATRIX POINTER
;OUTPUT:A = HEIGTH
;	HL = POINTER TO HEIGTH BYTE

	CSEG
	PUBLIC	HEIGTH

HEIGTH:	LD	L,C
	LD	H,B
	CALL	ADDRSQR
	LD	A,(HL)
	RET


