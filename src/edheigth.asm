
	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	EDITOR.INC
	INCLUDE	GEOMETRY.INC
	INCLUDE	KBD.INC

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	EDHEIGTH
	EXTRN	VDPSYNC,EDINIT,NEWHEIGTH,GRID

EDHEIGTH:
	CALL	EDINIT
	LD	DE,HEIGTHBUF
	CALL	NEWHEIGTH

	LD	A,TILPAGE
	LD	(ACPAGE),A
	CALL	GRID			;SHOW ISOMETRIC GRID

	XOR	A
	LD	D,A
	LD	E,A
	LD	(H.TILE),DE
	LD	(H.ZVAL),A

	LD	HL,0
	ADD	HL,SP
	LD	(EDSTACK),HL		;SAVE SP FOR CANCEL OPERATIONS

	;MAIN EDITOR LOOP
EDLOOP: CALL	VDPSYNC			;WAIT TO THE VDP
	CALL	SELTILE			;SELECT BEGINING OF SQUARE
	JR	EDLOOP

	RET

	DSEG
HEIGTHBUF:	DS	HEIGTHSIZ
H.TILE:		DW	0
H.ZVAL:		DB	0
EDSTACK:	DW	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CSEG

EXIT:	LD	HL,(EDSTACK)		;LONGJMP FOR EXITING OF EDITOR.
	LD	SP,HL
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	WRLD2SCR

HTILE2COORD:
	LD	DE,(H.TILE)
	LD	BC,CENTRAL.P1
	CALL	WRLD2SCR		;WE KNOW IT'LL RETURN POSITIVE NUMBERS
	LD	D,L			;AND SMALLER THAN 256
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: H.TILE = ACTUAL TILE SELECTED
;	H.ZVAL = ACTUAL Z VALUE SELECTED
;OUTPUT:H.TILE = OUTPUT TILE
;	H.ZVAL = OUTPUT Z VALUE

	CSEG
	EXTRN	MOVEUC,KEY2DIR,TILESPRITE,GETKEV

SELTILE:CALL	HTILE2COORD
	LD	BC,(H.ZVAL)
	CALL	TILESPRITE
	CALL	GETKEV

	CP	KB_ESC
	CALL	Z,EXIT

	CP	KB_CRTL
	JP	Z,SELZVAL

	CALL	KEY2DIR
	JR	C,SELTILE
	LD	DE,(H.TILE)
	CALL	MOVEUC

	LD	A,-1			;CHECK LIMITS
	CP	D
	JR	Z,SELTILE
	CP	E
	JR	Z,SELTILE
	LD	A,MAXISOX
	CP	D
	JR	Z,SELTILE
	LD	A,MAXISOY
	CP	E
	JR	Z,SELTILE

	LD	(H.TILE),DE
	JR	SELTILE


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: H.TILE = ACTUAL TILE SELECTED
;	H.ZVAL = ACTUAL Z VALUE SELECTED
;OUTPUT:H.ZVAL = OUTPUT Z VALUE

	CSEG
	EXTRN	MOVIEUC,GETKEV,KEY2DIR,MOVEUC


SELZVAL:CALL	HTILE2COORD
	LD	BC,(H.ZVAL)
	CALL	TILESPRITE
	CALL	GETKEV			;WAIT NEXT KEYBOARD EVENT
	CP	KB_CRTL + 128		;EXIT OF ZMODE RELEASING THE CRTL
	RET	Z

	CALL	KEY2DIR
	JR	C,SELZVAL
	LD	DE,(H.ZVAL)
	CALL	MOVIEUC			;INVERSE EUCLIDEAN MOVEMENT
	LD	A,E
	CP	-1
	JR	Z,SELZVAL		;CHECK LIMITS
	CP	MAXHEIGTH
	JR	Z,SELZVAL
	LD	(H.ZVAL),A
	JR	SELZVAL


