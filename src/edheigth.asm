
	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	EDITOR.INC
	INCLUDE	GEOMETRY.INC
	INCLUDE	KBD.INC
	INCLUDE	VDP.INC

MASKX	EQU	40H

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	EDHEIGTH
	EXTRN	VDPSYNC,EDINIT,GRID

EDHEIGTH:
	CALL	EDINIT
	LD	DE,HEIGTHBUF
	CALL	NEWHEIGTH

	LD	A,TILPAGE
	LD	(ACPAGE),A
	CALL	GRID			;SHOW ISOMETRIC GRID

	XOR	A
	LD	D,A
	LD	E,A
	LD	(H.TILE),DE
	LD	(H.ZVAL),A

	LD	HL,0
	ADD	HL,SP
	LD	(EDSTACK),HL		;SAVE SP FOR CANCEL OPERATIONS

	;MAIN EDITOR LOOP
EDLOOP: CALL	VDPSYNC			;WAIT TO THE VDP
	CALL	SELTILE			;SELECT BEGINING OF SQUARE
	CALL	SELREGION		;SELECT SIZE OF THE SQUARE
	CALL	SELZVAL
	JR	EDLOOP

	RET

	DSEG
HEIGTHBUF:	DS	HEIGTHSIZ
H.TILE:		DW	0
H.ZVAL:		DB	0
H.SQRSIZ:	DW	0
EDSTACK:	DW	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG

EXIT:	LD	HL,(EDSTACK)		;LONGJMP FOR EXITING OF EDITOR.
	LD	SP,HL
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = INPUT TILE POSITION

	CSEG
	EXTRN	WRLD2SCR

TIL2SCR:
	LD	BC,CENTRAL.P1
	CALL	WRLD2SCR		;WE KNOW IT'LL RETURN POSITIVE NUMBERS
	LD	D,L			;AND SMALLER THAN 256
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = ACTUAL TILE SELECTED
;OUTPUT:HL = OUTPUT TILE

	CSEG
	EXTRN	MOVEUC,KEY2DIR,TILESPRITE,GETKEV

SELTILE:LD	DE,(H.TILE)
	CALL	TIL2SCR			;CALCULATE SCREEN COORDENATES OF TILE
	LD	C,0
	CALL	TILESPRITE
	CALL	GETKEV

	CP	KB_ESC			;ESC EXITS FROM THE EDITOR
	CALL	Z,EXIT

	CP	KB_SPACE		;SPACE SELECTS THE TILE
	RET	Z

	CALL	KEY2DIR
	JR	C,SELTILE
	LD	DE,(H.TILE)
	CALL	MOVEUC

	LD	A,-1			;CHECK LIMITS
	CP	D
	JR	Z,SELTILE
	CP	E
	JR	Z,SELTILE
	LD	A,MAXISOX
	CP	D
	JR	Z,SELTILE
	LD	A,MAXISOY
	CP	E
	JR	Z,SELTILE

	LD	(H.TILE),DE
	JR	SELTILE

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: H.TILE = ACTUAL TILE SELECTED
;	H.ZVAL = ACTUAL Z VALUE SELECTED
;OUTPUT:H.ZVAL = OUTPUT Z VALUE

	CSEG
	EXTRN	GETKEV,KEY2DIR,MOVIEUC

SELZVAL:LD	DE,(H.TILE)
	CALL	TIL2SCR			;CALCULATE SCREEN COORDENATES OF TILE
	LD	BC,(H.ZVAL)
	CALL	TILESPRITE
	CALL	GETKEV			;WAIT NEXT KEYBOARD EVENT
	CP	KB_SPACE		;EXIT OF ZMODE PRESSING SPACE
	RET	Z

	CALL	KEY2DIR
	JR	C,SELZVAL
	LD	DE,(H.ZVAL)
	CALL	MOVIEUC			;INVERSE EUCLIDEAN MOVEMENT
	LD	A,E
	CP	-1
	JR	Z,SELZVAL		;CHECK LIMITS
	CP	MAXHEIGTH
	JR	Z,SELZVAL
	LD	(H.ZVAL),A
	JR	SELZVAL

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: (H.TILE) = LEFT-UPCORNER
;OUTPUT:(H.SQRSIZ) = SIZE OF THE REGION

	CSEG
	EXTRN	VDPSYNC,GETCHAR,KEY2DIR,MOVEUC

SELREGION:
	LD	DE,0101H
	LD	(H.SQRSIZ),DE

R.LOOP:	CALL	MARKREGION
	CALL	VDPSYNC
	CALL	GETCHAR
	PUSH	AF
	CALL	MARKREGION
	POP	AF

	CP	KB_SPACE		;SPACE SELECTS THE ACTUAL SIZE
	JR	NZ,R.DIR
	CALL	VDPSYNC
	RET

R.DIR:	CALL	KEY2DIR
	JR	C,R.LOOP
	LD	DE,(H.SQRSIZ)
	CALL	MOVEUC			;EUCLIDEAN MOVEMENT

	XOR	A			;CHECK LIMITS
	CP	D
	JR	Z,R.LOOP
	CP	E
	JR	Z,R.LOOP
	LD	BC,(H.TILE)		;DON'T SELECT FAR AWAY OF MAXIMUM
	LD	A,D			;VALUES
	ADD	A,B
	CP	MAXISOX+1
	JR	Z,R.LOOP
	LD	A,E
	ADD	A,C
	CP	MAXISOY+1
	JR	Z,R.LOOP
	LD	(H.SQRSIZ),DE
	JR	R.LOOP

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = UP-LEFT CORNER OF REGION
;	BC = SQUARE SIZE

	CSEG

MARKREGION:
	LD	DE,(H.TILE)
	LD	BC,(H.SQRSIZ)

MR.Y:	PUSH	BC
	PUSH	DE

MR.X:	PUSH	BC
	PUSH	DE
	CALL	MARKTILE
	POP	DE
	INC	D
	POP	BC
	DJNZ	MR.X

	POP	DE
	INC	E
	POP	BC
	DEC	C
	JR	NZ,MR.Y
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = ACTUAL TILE

	CSEG
	EXTRN	LMMM,VDPPAGE

MARKTILE:
	CALL	TIL2SCR
	LD	A,MASKPAGE
	LD	(VDPPAGE),A
	LD	A,LOGXOR
	LD	(LOGOP),A
	LD	HL,MASKX*256 + MASKY
	LD	BC,16*256 + 8
	JP	LMMM

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = POINTER TO HEIGTH BUFFER

	CSEG
	EXTRN	RESETHEIGTH

NEWHEIGTH:
	PUSH	DE
	CALL	RESETHEIGTH
	POP	HL
	LD	(SQR.BUF),HL
	INC	HL
	LD	(SQR.PTR),HL
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	H.CRUNCH

ADDSQUARE:
	LD	HL,(SQR.BUF)
	INC	(HL)
	LD	HL,(SQR.BUF)
	LD	DE,(H.TILE)
	LD	BC,(H.SQRSIZ)
	LD	A,(H.ZVAL)
	CALL	H.CRUNCH
	RET

	DSEG
SQR.PTR:	DW	0
SQR.BUF:	DW	0


