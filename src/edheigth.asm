
	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	EDITOR.INC
	INCLUDE	GEOMETRY.INC
	INCLUDE	KBD.INC

MASKX		EQU	40H
NUMCOORD	EQU	00C8H

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	EDHEIGTH
	EXTRN	HGTHMATRIX,HMATRIX,ED.SCREEN,VDPSYNC,EDINIT

EDHEIGTH:
	CALL	EDINIT			;THIS FUNCTION MUST BE CALLED AFTER
	XOR	A			;A CALL TO NEWHEIGTH
	LD	D,A
	LD	E,A
	LD	(H.TILE),DE
	LD	(H.ZVAL),A

	LD	HL,0
	ADD	HL,SP
	LD	(EDSTACK),HL		;SAVE SP FOR CANCEL OPERATIONS

	;MAIN EDITOR LOOP
EDLOOP:	LD	BC,HGTHMATRIX
	LD	DE,(SQR.BUF)
	CALL	HMATRIX			;GENERATE THE HEIGTH MATRIX
	CALL	ED.SCREEN
	CALL	VDPSYNC			;WAIT TO THE VDP
	CALL	SELTILE			;SELECT BEGINING OF SQUARE
	CALL	SELREGION		;SELECT SIZE OF THE SQUARE
	CALL	SELZVAL			;SELECT Z VALUE
	CALL	ADDSQUARE		;ADD HEIGTH SQUARE
	JR	EDLOOP

	DSEG
H.TILE:		DW	0
H.ZVAL:		DB	0
H.SQRSIZ:	DW	0
EDSTACK:	DW	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	ED.SCREEN

CANCEL:	LD	HL,(EDSTACK)		;LONGJMP TO EDITOR LOOP. CANCEL
	LD	SP,HL			;ANY OPERATION
	JR	EDLOOP

EXIT:	LD	HL,(EDSTACK)		;LONGJMP FOR EXITING OF EDITOR.
	LD	SP,HL
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = INPUT TILE POSITION

	CSEG
	EXTRN	WRLD2SCR

TIL2SCR:EX	DE,HL
	LD	DE,CENTRAL.P1X
	LD	BC,CENTRAL.P1Y		;WE KNOW IT'LL RETURN POSITIVE NUMBERS
	XOR	A
	CALL	WRLD2SCR		;AND SMALLER THAN 256
	LD	A,L			;ADJUST THE POSITION
	SUB	8			;BECAUSE WRLD2SCR RETURNS THE
	LD	D,A			;CENTRAL POSITION OF TILE
	LD	A,E
	SUB	4
	LD	E,A
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = ACTUAL TILE SELECTED
;OUTPUT:HL = OUTPUT TILE

	CSEG
	EXTRN	HEIGTH,MOVEUC,KEY2DIR,TILESPRITE,ED.KPRESS

SELTILE:LD	DE,(H.TILE)
	CALL	TIL2SCR			;CALCULATE SCREEN COORDENATES OF TILE
	PUSH	DE
	LD	DE,(H.TILE)
	LD	BC,HGTHMATRIX
	CALL	HEIGTH			;CALCULATE HEIGTH OF THE TILE
	LD	C,A
	POP	DE
	CALL	TILESPRITE
	CALL	ZVAL2SPR		;SHOW THE HEIGTH IN THIS TILE
	CALL	ED.KPRESS

	CP	KB_ESC			;ESC EXITS FROM THE EDITOR
	CALL	Z,EXIT

	CP	KB_SPACE		;SPACE SELECTS THE TILE
	RET	Z

	CP	KB_DEL			;DEL DELETE LAST SQUARE
	JR	NZ,T.KEY
	CALL	DELSQUARE
	JR	SELTILE

T.KEY:	CALL	KEY2DIR
	JR	C,SELTILE
	LD	DE,(H.TILE)
	CALL	MOVEUC

	LD	A,-1			;CHECK LIMITS
	CP	D
	JR	Z,SELTILE
	CP	E
	JR	Z,SELTILE
	LD	A,MAXISOX
	CP	D
	JR	Z,SELTILE
	LD	A,MAXISOY
	CP	E
	JR	Z,SELTILE

	LD	(H.TILE),DE
	JR	SELTILE

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: H.TILE = ACTUAL TILE SELECTED
;	H.ZVAL = ACTUAL Z VALUE SELECTED
;OUTPUT:H.ZVAL = OUTPUT Z VALUE

	CSEG
	EXTRN	NUM2SPR,VDPSYNC,KPRESS,KEY2DIR,MOVIEUC

SELZVAL:CALL	MARKREGION		;MARK REGION
	CALL	VDPSYNC

Z.LOOP:	LD	DE,(H.TILE)
	CALL	TIL2SCR			;CALCULATE SCREEN COORDENATES OF TILE
	LD	BC,(H.ZVAL)
	CALL	TILESPRITE
	LD	A,(H.ZVAL)
	LD	E,A
	LD	BC,NUMCOORD
	CALL	NUM2SPR			;SHOW ACTUAL VALUE OF H.ZVAL
	CALL	KPRESS			;WAIT NEXT KEYBOARD EVENT

	CP	KB_SPACE		;EXIT OF ZMODE PRESSING SPACE
	JR	NZ,Z.ESC
	CALL	MARKREGION		;UNMARK REGION
        RET

Z.ESC:	CP	KB_ESC			;ESC CANCEL OPERATION
	CALL	Z,CANCEL

	CALL	KEY2DIR
	JR	C,Z.LOOP
	LD	DE,(H.ZVAL)
	CALL	MOVIEUC			;INVERSE EUCLIDEAN MOVEMENT
	LD	A,E
	CP	-1
	JR	Z,Z.LOOP		;CHECK LIMITS
	CP	MAXISOZ
	JR	Z,Z.LOOP
	LD	(H.ZVAL),A
	JR	Z.LOOP

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: (H.TILE) = LEFT-UPCORNER
;OUTPUT:(H.SQRSIZ) = SIZE OF THE REGION

	CSEG
	EXTRN	VDPSYNC,KPRESS,KEY2DIR,MOVEUC

SELREGION:
	LD	DE,0101H
	LD	(H.SQRSIZ),DE

R.LOOP:	CALL	MARKREGION		;MARK THE SQUARE REGION
	CALL	SQR2SPR			;SHOW THE HEIGTH BUFFER USAGE
	CALL	VDPSYNC			;WAIT TO THE VDP
	CALL	KPRESS			;WAIT A NEW KEY
	PUSH	AF
	CALL	MARKREGION		;UNMARK THE REGION
	POP	AF

	CP	KB_ESC			;ESC CANCEL OPERATION
	CALL	Z,CANCEL

	CP	KB_SPACE		;SPACE SELECTS THE ACTUAL SIZE
	JR	NZ,R.DIR
	CALL	VDPSYNC
	RET

R.DIR:	CALL	KEY2DIR
	JR	C,R.LOOP
	LD	DE,(H.SQRSIZ)
	CALL	MOVEUC			;EUCLIDEAN MOVEMENT

	XOR	A			;CHECK LIMITS
	CP	D
	JR	Z,R.LOOP
	CP	E
	JR	Z,R.LOOP
	LD	BC,(H.TILE)		;DON'T SELECT FAR AWAY OF MAXIMUM
	LD	A,D			;VALUES
	ADD	A,B
	CP	MAXISOX+1
	JR	Z,R.LOOP
	LD	A,E
	ADD	A,C
	CP	MAXISOY+1
	JR	Z,R.LOOP
	LD	(H.SQRSIZ),DE
	JR	R.LOOP




;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = UP-LEFT CORNER OF REGION
;	BC = SQUARE SIZE

	CSEG

MARKREGION:
	LD	DE,(H.TILE)
	LD	BC,(H.SQRSIZ)

MR.Y:	PUSH	BC
	PUSH	DE

MR.X:	PUSH	BC
	PUSH	DE
	CALL	MARKTILE
	POP	DE
	INC	D
	POP	BC
	DJNZ	MR.X

	POP	DE
	INC	E
	POP	BC
	DEC	C
	JR	NZ,MR.Y
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = ACTUAL TILE

	CSEG
	EXTRN	LMMM,VDPPAGE

MARKTILE:
	CALL	TIL2SCR
	LD	A,MASKPAGE
	LD	(VDPPAGE),A
	LD	A,LOGXOR
	LD	(LOGOP),A
	LD	HL,MASKX*256 + MASKY
	LD	BC,16*256 + 8
	JP	LMMM

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = POINTER TO HEIGTH BUFFER

	CSEG
	EXTRN	MULTEA
	PUBLIC	NEWHEIGTH

NEWHEIGTH:
	LD	(SQR.BUF),DE
	LD	A,(DE)
	INC	DE
	PUSH	DE
	LD	E,HEIGTHCMD
	CALL	MULTEA
	POP	DE
	ADD	HL,DE
	LD	(SQR.PTR),HL
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: (H.TILE) = ACTUAL TILE POSITION

	CSEG
	EXTRN	HEIGTH,NUM2SPR

ZVAL2SPR:
	LD	DE,(H.TILE)
	LD	BC,HGTHMATRIX			;TODO: DON'T USE IT DIRECTLY
	CALL	HEIGTH
	LD	E,A
	LD	BC,NUMCOORD
	JP	NUM2SPR

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: (SQR.PTR) = POINTER TO FIRST FREE POSITION
;	(SQR.BUF) = POINTER TO BUFFER BEGINNING

	CSEG
	EXTRN	NUM2SPR

SQR2SPR:LD	HL,(SQR.PTR)
	LD	DE,(SQR.BUF)
	OR	A
	SBC	HL,DE
	LD	E,L
	LD	BC,NUMCOORD
	JP	NUM2SPR

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG

DELSQUARE:
	LD	HL,(SQR.BUF)
	LD	A,(HL)
	OR	A
	RET	Z			;THERE IS NOTHING TO DELETE

	DEC	(HL)			;DECREMENT COUNT
	LD	HL,(SQR.PTR)
	LD	DE,HEIGTHCMD
	OR	A
	SBC	HL,DE
	LD	(SQR.PTR),HL		;DECREMENT POINTER TO 1ST FREE POS

	LD	BC,HGTHMATRIX		;UPDATE HEIGTH MATRIX
	LD	DE,(SQR.BUF)
	CALL	HMATRIX
	JP	ED.SCREEN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	ERRNO,ED.ERROR,H.CRUNCH

ADDSQUARE:
	LD	HL,(SQR.BUF)
	LD	DE,HEIGTHSIZ
	ADD	HL,DE			;MAX ADDRESS IN BUFFER
	EX	DE,HL			;DE = MAX ADDRESS IN BUFFER
	LD	HL,(SQR.PTR)
	LD	BC,HEIGTHCMD
	ADD	HL,BC			;HL = LAST ADDRESS USED IN BUFFER
	EX	DE,HL
	OR	A
	SBC	HL,DE
	JR	NC,A.OK
	LD	A,E.NOMEM
	LD	(ERRNO),A
	CALL	ED.ERROR
	JP	CANCEL			;CANCEL IF NOT ENOUGH ROOM

A.OK:	LD	HL,(SQR.BUF)		;INCREMENT NUMBER OF SQUARES
	INC	(HL)
	LD	HL,(SQR.PTR)
	LD	DE,(H.TILE)
	LD	BC,(H.SQRSIZ)
	LD	A,(H.ZVAL)
	CALL	H.CRUNCH
	LD	(SQR.PTR),HL
	RET

	DSEG
	PUBLIC	HGTBUF
SQR.PTR:	DW	0
HGTBUF:
SQR.BUF:	DW	0


