
	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	LEVEL.INC
	INCLUDE	EVENT.INC

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	EDTILE
	EXTRN	LOADSET,EDINIT,VDPSYNC,LISTEN,CARTPAGE

EDTILE:	CALL	EDINIT
	LD	A,(SET)
	LD	E,A
	CALL	LOADSET			;LOAD GRAPHICS FOR SELECTED SET

ED.LOOP:LD	E,LEVELPAGE
	CALL	CARTPAGE		;SET LEVEL DEFINITION
	CALL	GETTDATA		;GET PATTERN STACK INFORMATION
	CALL	SHOWSCR			;SHOW THE EDITOR SCREEN
	CALL	VDPSYNC			;WAIT TO THE VDP
	LD	DE,RECEIVERS
	CALL	LISTEN			;LISTEN MOUSE EVENTS
	JR	NZ,ED.LOOP
	RET

RECEIVERS:
	DB	80,30,30,8
	DW	CHANGETILE
	DB	80,30,38,8
	DW	CHANGEPAL
	DB	80,30,46,8
	DW	CHANGESET
	DB	80,16,78,8
	DW	PUTPATTERN1
	DB	80,16,86,8
	DW	PUTPATTERN2
	DB	80,16,94,8
	DW	PUTPATTERN3
	DB	80,16,102,8
	DW	PUTPATTERN4
	DB	80,16,110,8
	DW	PUTPATTERN5
	DB	80,16,118,8
	DW	PUTPATTERN6
	DB	80,30,142,8
	DW	CHANGEHEIGHT
	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	HMMV

CLEANSTACK:
	LD	DE,80*256 + 78
	LD	BC,16*256 + TILEYSIZ*8
	XOR	A
	LD	(FORCLR),A
	JP	HMMV			;BLACK RECTANGLE



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	GETNUMPAT

GETTDATA:
	LD	A,(TILE)
	LD	E,A
	CALL	GETTILE			;GET THE POINTER TO THE FLOOR
	LD	(TPTR),HL		;SAVE FLOOR POINTER
	LD	A,(HL)
	LD	(HEIGHT),A

	LD	DE,TILE.MAP
	ADD	HL,DE
	PUSH	HL
	PUSH	HL
	PUSH	HL
	PUSH	HL
	PUSH	HL

	CALL	GETNUMPAT		;CALCULATE NUMPAT1
	LD	(NUMPAT1),A

	POP	HL
	LD	DE,NR_LAYERS
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT2),A		;CALCULATE NUMPAT2

	POP	HL
	LD	DE,NR_LAYERS*2
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT3),A		;CALCULATE NUMPAT3

	POP	HL
	LD	DE,NR_LAYERS*3
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT4),A		;CALCULATE NUMPAT4

	POP	HL
	LD	DE,NR_LAYERS*4
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT5),A		;CALCULATE NUMPAT5

	POP	HL
	LD	DE,NR_LAYERS*5
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT6),A		;CALCULATE NUMPAT6
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	SETPAL,PRINTF,LOCATE,GLINES,DRAWSTACKS

SHOWSCR:CALL	CLEANSTACK
	LD	DE,0
	CALL	LOCATE

	LD	DE,(PAL)
	CALL	GETPAL
	CALL	SETPAL			;LOAD THE SELECTED PALETE

	LD	H,0
	LD	A,(HEIGHT)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT6)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT5)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT4)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT3)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT2)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT1)
	LD	L,A
	PUSH	HL
	LD	A,(SET)
	LD	L,A
	PUSH	HL
	LD	A,(PAL)
	LD	L,A
	PUSH	HL
	LD	A,(TILE)
	LD	L,A
	PUSH	HL
	LD	DE,FMT
	CALL	PRINTF			;PRINT THE TEXT INFORMATION

	LD	DE,TILEG
	LD	C,15
	CALL	GLINES

	LD	HL,(TPTR)
	LD	DE,TILE.MAP
	ADD	HL,DE
	LD	E,TILEYSIZ
	LD	BC,80*256 + 78
	JP	DRAWSTACKS


;	       REP  X0  Y0    X1  Y1 IX0 IY0 IX1 IY1
TILEG:	DB	4,  80, 30,  110, 30,  0,  8,  0,  8
	DB	2,  80, 30,   80, 54, 30,  0, 30,  0
	DB	7,  80, 78,   96, 78,  0,  8,  0,  8
	DB	2,  80, 78,   80,126, 16,  0, 16,  0
	DB	2,  80,142,  110,142,  0,  8,  0,  8
	DB	2,  80,142,   80,150, 30,  0, 30,  0
	DB	0

FMT:	DB	10,10,10,10
	DB	9,9,"     TILE",9,"%03d",10
	DB	9,9,"     PALETE",9,"%3d",10
	DB	9,9,"     SET",9,"%3d",10
	DB	10,10,10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	10,10
	DB	9,9,"     HEIGHT",9,"%3d",0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = EVENT

	CSEG

CHANGETILE:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT TILE NUMBER
	LD	A,(TILE)
	JR	NZ,F.DEC
	CP	NR_TILES-1
	RET	Z
	INC	A
	JR	F.RET

F.DEC:	OR	A
	RET	Z
	DEC	A			;BUTTON 2 DECREMENT TILE NUMBER
F.RET:	LD	(TILE),A
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: A = EVENT

	CSEG

CHANGEPAL:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT PALETE NUMBER
	LD	A,(PAL)
	JR	NZ,P.DEC
	CP	NR_PALETES-1
	RET	Z
	INC	A
	JR	P.RET

P.DEC:	OR	A			;BUTTON 2 DECREMENT PALETE NUMBER
	RET	Z
	DEC	A
P.RET:	LD	(PAL),A
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: A = EVENT

	CSEG
	EXTRN	LOADSET

CHANGESET:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT SET NUMBER
	LD	A,(SET)
	JR	NZ,S.DEC
	CP	NR_PATSET-1
	RET	Z
	INC	A
	JR	S.RET

S.DEC:	OR	A			;BUTTON 2 DECREMENT SET NUMBER
	RET	Z
	DEC	A
S.RET:	LD	(SET),A
	LD	E,A			;THERE IS A SET CHANGE, SO UPDATE
	JP	LOADSET			;THE GRAPHICS

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = EVENT

	CSEG
	EXTRN	PATEVENT

PUTPATTERN1:
	LD	BC,(NUMPAT1)
	LD	HL,(TPTR)
	LD	DE,TILE.MAP
	JP	PATEVENT

PUTPATTERN2:
	LD	BC,(NUMPAT2)
	LD	HL,(TPTR)
	LD	DE,TILE.MAP+NR_LAYERS
	JP	PATEVENT

PUTPATTERN3:
	LD	BC,(NUMPAT3)
	LD	HL,(TPTR)
	LD	DE,TILE.MAP+NR_LAYERS*2
	JP	PATEVENT

PUTPATTERN4:
	LD	BC,(NUMPAT4)
	LD	HL,(TPTR)
	LD	DE,TILE.MAP+NR_LAYERS*3
	JP	PATEVENT

PUTPATTERN5:
	LD	BC,(NUMPAT5)
	LD	HL,(TPTR)
	LD	DE,TILE.MAP+NR_LAYERS*4
	JP	PATEVENT

PUTPATTERN6:
	LD	BC,(NUMPAT6)
	LD	HL,(TPTR)
	LD	DE,TILE.MAP+NR_LAYERS*5
	JP	PATEVENT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = EVENT

	CSEG

CHANGEHEIGHT:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT SET NUMBER
	LD	A,(HEIGHT)
	JR	NZ,H.DEC
	CP	TILEYSIZ
	RET	Z
	INC	A
	JR	H.RET

H.DEC:	OR	A			;BUTTON 2 DECREMENT SET NUMBER
	RET	Z
	DEC	A
H.RET:	LD	HL,(TPTR)
	LD	(HL),A
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	DSEG
TPTR:	DW	0
TILE:	DB	0
SET:	DB	0
PAL:	DB	0
NUMPAT1:DB	0
NUMPAT2:DB	0
NUMPAT3:DB	0
NUMPAT4:DB	0
NUMPAT5:DB	0
NUMPAT6:DB	0
HEIGHT:	DB	0

