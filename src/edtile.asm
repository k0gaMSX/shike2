
	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	LEVEL.INC
	INCLUDE	EVENT.INC

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	ED.TILE
	EXTRN	EDINIT,VDPSYNC,LISTEN,CARTPAGE

ED.TILE:CALL	EDINIT

ED.LOOP:LD	E,LEVELPAGE
	CALL	CARTPAGE		;SET LEVEL DEFINITION
	CALL	GETTDATA		;GET PATTERN STACK INFORMATION
	CALL	SHOWSCR			;SHOW THE EDITOR SCREEN
	CALL	VDPSYNC			;WAIT TO THE VDP
	LD	DE,RECEIVERS
	CALL	LISTEN			;LISTEN MOUSE EVENTS
	JR	NZ,ED.LOOP
	RET

	EXTRN	PALEVENT,SETEVENT

RECEIVERS:
	DB	80,30,30,8
	DW	CHANGETILE
	DB	80,30,38,8
	DW	PALEVENT
	DB	80,30,46,8
	DW	SETEVENT
	DB	80,16,78,8
	DW	PUTPATTERN1
	DB	80,16,86,8
	DW	PUTPATTERN2
	DB	80,16,94,8
	DW	PUTPATTERN3
	DB	80,16,102,8
	DW	PUTPATTERN4
	DB	80,16,110,8
	DW	PUTPATTERN5
	DB	80,16,118,8
	DW	PUTPATTERN6
	DB	80,30,142,8
	DW	CHANGEHEIGHT
	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	HMMV

CLEANSTACK:
	LD	DE,80*256 + 78
	LD	BC,16*256 + TILEYSIZ*8
	XOR	A
	LD	(FORCLR),A
	JP	HMMV			;BLACK RECTANGLE



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	EDTILE,GETNUMPAT

GETTDATA:
	LD	A,(EDTILE)
	OR	A
	JR	NZ,G.1
	XOR	A
	LD	(NUMPAT1),A		;TILE 0 DOESN'T HAVE DATA
	LD	(NUMPAT2),A
	LD	(NUMPAT3),A
	LD	(NUMPAT4),A
	LD	(NUMPAT5),A
	LD	(NUMPAT6),A
	RET

G.1:	LD	E,A
	CALL	GETTILE			;GET THE POINTER TO THE FLOOR
	LD	(TPTR),HL		;SAVE FLOOR POINTER
	LD	A,(HL)
	LD	(HEIGHT),A

	LD	DE,TILE.MAP
	ADD	HL,DE
	PUSH	HL
	PUSH	HL
	PUSH	HL
	PUSH	HL
	PUSH	HL

	CALL	GETNUMPAT		;CALCULATE NUMPAT1
	LD	(NUMPAT1),A

	POP	HL
	LD	DE,NR_LAYERS
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT2),A		;CALCULATE NUMPAT2

	POP	HL
	LD	DE,NR_LAYERS*2
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT3),A		;CALCULATE NUMPAT3

	POP	HL
	LD	DE,NR_LAYERS*3
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT4),A		;CALCULATE NUMPAT4

	POP	HL
	LD	DE,NR_LAYERS*4
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT5),A		;CALCULATE NUMPAT5

	POP	HL
	LD	DE,NR_LAYERS*5
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT6),A		;CALCULATE NUMPAT6
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	EDTILE,EDSET,EDPAL,PRINTF,LOCATE,GLINES,DRAWSTACKS

SHOWSCR:CALL	CLEANSTACK
	LD	DE,0
	CALL	LOCATE

	LD	H,0
	LD	A,(HEIGHT)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT6)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT5)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT4)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT3)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT2)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT1)
	LD	L,A
	PUSH	HL
	LD	A,(EDSET)
	LD	L,A
	PUSH	HL
	LD	A,(EDPAL)
	LD	L,A
	PUSH	HL
	LD	A,(EDTILE)
	LD	L,A
	PUSH	HL
	LD	DE,FMT
	CALL	PRINTF			;PRINT THE TEXT INFORMATION

	LD	DE,TILEG
	LD	C,15
	CALL	GLINES

	LD	A,(EDTILE)		;TILE 0 IS THE EMPTY ONE, SO DON'T
	OR	A			;PRINT IT
	RET	Z

	LD	HL,(TPTR)
	LD	DE,TILE.MAP
	ADD	HL,DE
	LD	E,TILEYSIZ
	LD	BC,80*256 + 78
	JP	DRAWSTACKS


;	       REP  X0  Y0    X1  Y1 IX0 IY0 IX1 IY1
TILEG:	DB	4,  80, 30,  110, 30,  0,  8,  0,  8
	DB	2,  80, 30,   80, 54, 30,  0, 30,  0
	DB	7,  80, 78,   96, 78,  0,  8,  0,  8
	DB	2,  80, 78,   80,126, 16,  0, 16,  0
	DB	2,  80,142,  110,142,  0,  8,  0,  8
	DB	2,  80,142,   80,150, 30,  0, 30,  0
	DB	0

FMT:	DB	10,10,10,10
	DB	9,9,"     TILE",9,"%03d",10
	DB	9,9,"     PALETE",9,"%3d",10
	DB	9,9,"     SET",9,"%3d",10
	DB	10,10,10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",10
	DB	10,10
	DB	9,9,"     HEIGHT",9,"%3d",0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = EVENT

	CSEG
	EXTRN	EDTILE

CHANGETILE:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT TILE NUMBER
	LD	A,(EDTILE)
	JR	NZ,F.DEC
	CP	NR_TILES
	RET	Z
	INC	A
	JR	F.RET

F.DEC:	OR	A
	RET	Z
	DEC	A			;BUTTON 2 DECREMENT TILE NUMBER
F.RET:	LD	(EDTILE),A
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = EVENT

	CSEG
	EXTRN	PATEVENT

PUTPATTERN1:
	LD	BC,(NUMPAT1)
	LD	DE,TILE.MAP
	JR	P.AUX

PUTPATTERN2:
	LD	BC,(NUMPAT2)
	LD	DE,TILE.MAP+NR_LAYERS
	JR	P.AUX

PUTPATTERN3:
	LD	BC,(NUMPAT3)
	LD	DE,TILE.MAP+NR_LAYERS*2
	JR	P.AUX

PUTPATTERN4:
	LD	BC,(NUMPAT4)
	LD	DE,TILE.MAP+NR_LAYERS*3
	JR	P.AUX

PUTPATTERN5:
	LD	BC,(NUMPAT5)
	LD	DE,TILE.MAP+NR_LAYERS*4
	JR	P.AUX

PUTPATTERN6:
	LD	BC,(NUMPAT6)
	LD	DE,TILE.MAP+NR_LAYERS*5

P.AUX:	EX	AF,AF'			;DON'T MODIFY TILE 0
	LD	A,(EDTILE)
	OR	A
	RET	Z
	EX	AF,AF'
	LD	HL,(TPTR)
	JP	PATEVENT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = EVENT

	CSEG
	EXTRN	EDTILE

CHANGEHEIGHT:
	EX	AF,AF'			;DON'T MODIFY TILE 0
	LD	A,(EDTILE)
	OR	A
	RET	Z
	EX	AF,AF'

	CP	MS_BUTTON1		;BUTTON 1 INCREMENT SET NUMBER
	LD	A,(HEIGHT)
	JR	NZ,H.DEC
	CP	TILEYSIZ
	RET	Z
	INC	A
	JR	H.RET

H.DEC:	OR	A			;BUTTON 2 DECREMENT SET NUMBER
	RET	Z
	DEC	A
H.RET:	LD	HL,(TPTR)
	LD	(HL),A
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	DSEG
TPTR:	DW	0
NUMPAT1:DB	0
NUMPAT2:DB	0
NUMPAT3:DB	0
NUMPAT4:DB	0
NUMPAT5:DB	0
NUMPAT6:DB	0
HEIGHT:	DB	0

