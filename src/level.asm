
	INCLUDE	SHIKE2.INC
	INCLUDE	EDITOR.INC
	INCLUDE	GEOMETRY.INC

LEFT.P1X	EQU	CENTRAL.P1X-80H
LEFT.P1Y	EQU	CENTRAL.P1Y-40H

RIGTH.P1X	EQU	CENTRAL.P1X+80H
RIGTH.P1Y	EQU	CENTRAL.P1Y+40H

UP.P1X		EQU	CENTRAL.P1X+80H
UP.P1Y		EQU	CENTRAL.P1Y-40H

DOWN.P1X	EQU	CENTRAL.P1X-80H
DOWN.P1Y	EQU	CENTRAL.P1Y+40H

LUP.P1X		EQU	CENTRAL.P1X
LUP.P1Y		EQU	CENTRAL.P1Y-80H

RDOWN.P1X	EQU	CENTRAL.P1X
RDOWN.P1Y	EQU	CENTRAL.P1Y+80H


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = LOCATION OF THE MAP IN THE LEVEL
;OUTPUT:HL = ADRESS OF THE ROOM IN THE LEVEL

	CSEG
	PUBLIC	ROOMADDR
	EXTRN	MULTEA

ROOMADDR:
	PUSH	DE
	LD	D,0
	LD	A,(LVLYSIZ)
	CALL	MULTEA
	POP	DE			;HL = Y*LVLYSIZ
	LD	E,D
	LD	D,0
	ADD	HL,DE			;HL = Y*LVLYSIZ + X
	LD	DE,LVLROOM
	ADD	HL,DE			;HL = LVLROOM + Y*LVLYSIZ + X
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = POSIITON OF THE ROOM
;OUTPUT:A = ROOM NUMBER

	CSEG
	PUBLIC	GETROOM

GETROOM:LD	A,-1			;ROOM = -1,-1 IS MARK OF INVALID ROOM
	CP	D
	JR	NZ,G.1
	CP	E
	JR	NZ,G.1
	LD	A,-1
	JR	G.RET

G.1:	CALL	ROOMADDR
	LD	A,(HL)
G.RET:	CP	-1
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = POSITION OF ROOM
;OUTPUT:HL = ADRESS OF ROOM MAP

	CSEG
	PUBLIC	ROOM2MAP
	EXTRN	MULTDEA

ROOM2MAP:
	CALL	GETROOM
	LD	HL,0
	RET	Z
	LD	DE,MAPSIZ
	CALL	MULTDEA				;HL = ROOM*MAPSIZ
	LD	DE,LVLMAP
	ADD	HL,DE				;HL = ROOM*MAPSIZ+LVLMAP
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = POSITION OF ROOM
;OUTPUT:HL = ADDRESS OF ROOM HEIGTH

	CSEG
	PUBLIC	ROOM2HGT
	EXTRN	MULTDEA

ROOM2HGT:
	CALL	GETROOM
	LD	HL,0
	RET	Z
	LD	DE,HEIGTHSIZ
	CALL	MULTDEA				;HL = ROOM*HEIGTHSIZ
	LD	DE,LVLHGT
	ADD	HL,DE				;HL = ROOM*HEIGTHSIZ+LVLHGT
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: A = NUMBER OF LEVEL


	CSEG
	PUBLIC	PUTLEVEL

PUTLEVEL:
	ADD	A,7
	OUT	(0FEH),A
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = ROOM LOCATION
;	B = LEVEL
;OUTPUT:HL = ROOM DATA WHEN IT IS VISIBLE, 0 IN OTHER CASE
;	Z = 1 WHEN ROOM IS NOT VISIBLE

	CSEG
	PUBLIC	GET_RDATA
	EXTRN	ADDAHL

GET_RDATA:
	LD	A,(LEVEL)
	CP	B
	JR	Z,G.NOK			;DIFFERENT LEVEL, SO NO HEIGTH

	CALL	LOOKUP
	JR	Z,G.NOK
	ADD	A,A
	PUSH	AF
	ADD	A,A
	POP	HL
	ADD	A,H
	LD	HL,G.DATA
	CALL	ADDAHL
	LD	A,1
	OR	A			;SET Z=0
	RET

G.NOK:	LD	HL,0			;WE KNOW Z=1
	RET


G.DATA:	DW	HGT.IN,CENTRAL.P1X,CENTRAL.P1Y
	DW	HGT.LEFT,LEFT.P1X,LEFT.P1Y
	DW	HGT.RIGTH,RIGTH.P1X,RIGTH.P1Y
	DW	HGT.UP,UP.P1X,UP.P1Y
	DW	HGT.DOWN,DOWN.P1X,DOWN.P1Y
	DW	HGT.LUP,LUP.P1X,LUP.P1Y
	DW	HGT.RDOWN,RDOWN.P1X,RDOWN.P1Y



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = ROOM POSITION
;OUTPUT:A = POSITION INTO THE ARRAY, OR -1 WHEN IT IS NOT FOUND
;	Z = 1 WHEN ROOM IS NOT DISPLAYED

	CSEG

LOOKUP:	LD	HL,ROOM.IN
	LD	BC,16

L.LOOP:	LD	A,E			;LOOK THE LOWER BYTE
	OR	80H			;(BIT 7 = 1 HELP SEARCHING)
	CPIR
	JR	NZ,L.NOT
	LD	A,D
	CPI				;AND NOW LOOK THE UPPER BYTE
	JR	Z,L.FOUND
	JP	PO,L.NOT

L.FOUND:LD	DE,ROOM.IN
	OR	A
	SBC	HL,DE
	CP	-1
	RET

L.NOT:	LD	A,-1
	CP	-1
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = ROOM LOCATION
;OUTPUT:DE = INPUT PARAMETER WHEN CORRECT, OR -1 IF INCORRECT

	CSEG

CHKROOM:LD	A,-1			;CHECK IF THE ROOM LOCATION IS CORRECT
	CP	D
	JR	Z,C.WRONG
	CP	E
	JR	Z,C.WRONG
	LD	A,(LVLYSIZ)
	CP	E
	JR	Z,C.WRONG
	LD	A,(LVLXSIZ)
	CP	D
	JR	Z,C.WRONG
	BIT	7,E			;MARK IT LIKE LOWER BYTE
	RET

C.WRONG:LD	DE,-1
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = ROOM LOCATION OF CENTRAL ROOM
;OUTPUT:ROOM.IN,ROOM.LEFT,ROOM.RIGTH,ROOM.UP,ROOM.DOWN,ROOM.LUP,ROOM.RDOWN,

	CSEG

GETVISIBLES:				;CALCULATE COORDENATES OF VISIBLES ROOMS
	PUSH	DE
	PUSH	DE
	PUSH	DE
	PUSH	DE
	PUSH	DE
	CALL	CHKROOM
	LD	(ROOM.IN),DE

	DEC	D
	CALL	CHKROOM
	LD	(ROOM.LEFT),DE

	POP	DE
	INC	D
	CALL	CHKROOM
	LD	(ROOM.RIGTH),DE

	POP	DE
	DEC	E
	CALL	CHKROOM
	LD	(ROOM.UP),DE

	POP	DE
	INC	E
	CALL	CHKROOM
	LD	(ROOM.DOWN),DE

	POP	DE
	DEC	E
	DEC	D
	CALL	CHKROOM
	LD	(ROOM.LUP),DE

	POP	DE
	INC	E
	INC	D
	CALL	CHKROOM
	LD	(ROOM.RDOWN),DE
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = LEVEL
;	DE = ROOM LOCATION

	CSEG
	PUBLIC	MOVECAMARA
	EXTRN	HMATRIX,PTRDE,MAP

MOVECAMARA:
	LD	(LEVEL),A
	LD	(ROOM),DE
	CALL	PUTLEVEL
	CALL	ROOM2MAP
	EX	DE,HL			;DE = MAP ADDRESS
	CALL	MAP			;MAP THE ROOM

	CALL	GETVISIBLES		;CALCULATE VISIBLES ROOMS

	LD	DE,HGT.IN
	LD	(H.PTR),DE
	LD	B,6
	LD	DE,ROOM.IN

M.LOOP:	PUSH	BC			;GET THE HEIGTH MATRIX FOR EACH
	PUSH	DE			;VISIBLE ROOM
	CALL	PTRDE
	RES	7,E
	CALL	ROOM2HGT
	EX	DE,HL
	LD	BC,(H.PTR)
	CALL	HMATRIX

	LD	HL,(H.PTR)
	LD	DE,HEIGTHMATRIXSIZ
	ADD	HL,DE
	LD	(H.PTR),HL
	POP	DE
	INC	DE
	POP	BC
	DJNZ	M.LOOP
	RET


	DSEG
H.PTR:	DW	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	DSEG


LEVEL:		DB	0
ROOM:		DW	0

HGT.IN:		DS	HEIGTHMATRIXSIZ
HGT.LEFT:	DS	HEIGTHMATRIXSIZ
HGT.RIGTH:	DS	HEIGTHMATRIXSIZ
HGT.UP:		DS	HEIGTHMATRIXSIZ
HGT.DOWN:	DS	HEIGTHMATRIXSIZ
HGT.LUP:	DS	HEIGTHMATRIXSIZ
HGT.RDOWN:	DS	HEIGTHMATRIXSIZ


ROOM.IN:	DW	0
ROOM.LEFT:	DW	0
ROOM.RIGTH:	DW	0
ROOM.UP:	DW	0
ROOM.DOWN:	DW	0
ROOM.LUP:	DW	0
ROOM.RDOWN:	DW	0

