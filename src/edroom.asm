
	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	LEVEL.INC
	INCLUDE	EVENT.INC

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	ED.ROOM
	EXTRN	ADDGLISTEN,PUTLPAGE,EDINIT,LISTEN,VDPSYNC

ED.ROOM:CALL	EDINIT
	LD	DE,R.PTR
	LD	BC,POSEVENT
	CALL	ADDGLISTEN

ED.LOOP:CALL	PUTLPAGE
	CALL	GETRDATA
	CALL	SHOWSCR
	CALL	VDPSYNC
	LD	DE,RECEIVERS
	CALL	LISTEN
	JR	NZ,ED.LOOP
	RET


RECEIVERS:
	DB	156,30,158,8
	DW	LEVELPAL
	DB	156,30,166,8
	DW	LEVELSET
R.PTR:	DS	64*6
	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	EDLEVEL,GETLEVEL

GETRDATA:
	LD	DE,(EDLEVEL)
	CALL	GETLEVEL
	RET	Z
	PUSH	HL
	POP	IY
	LD	A,(IY+LVL.PAL)
	LD	(PALETE),A
	LD	A,(IY+LVL.GFX)
	LD	(PATSET),A
	LD	(LPTR),IY
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	GLINES,PTRHL,PRINTF,LOCATE,EDLEVEL,GRID16,GETROOM

SHOWSCR:CALL	GRID16
	CALL	DRAWRMATRIX

	LD	DE,0
	CALL	LOCATE
	LD	HL,(LPTR)
	PUSH	HL
	LD	DE,TITLE
	CALL	PRINTF

	LD	DE,0054H
	CALL	LOCATE

	LD	DE,(EDLEVEL)
	LD	BC,(EDROOM)
	LD	A,2
	CALL	GETROOM
	CALL	PTRHL
	PUSH	HL

	LD	DE,(EDLEVEL)
	LD	BC,(EDROOM)
	LD	A,1
	CALL	GETROOM
	CALL	PTRHL
	PUSH	HL

	LD	HL,(PATSET)
	LD	H,0
	PUSH	HL

	LD	DE,(EDLEVEL)
	LD	BC,(EDROOM)
	XOR	A
	CALL	GETROOM
	CALL	PTRHL
	PUSH	HL

	LD	HL,(PALETE)
	LD	H,0
	PUSH	HL

	LD	H,0
	LD	DE,(EDROOM)
	LD	L,E
	PUSH	HL
	LD	L,D
	PUSH	HL
	LD	DE,ROOMI
	CALL	PRINTF

	LD	C,15
	LD	DE,ROOMG
	JP	GLINES


;	       REP  X0  Y0    X1  Y1 IX0 IY0 IX1 IY1
ROOMG:	DB	3, 156,158,  186,158,  0,  8,  0,  8
	DB	2, 156,158,  156,174, 30,  0, 30,  0
	DB	0

TITLE:	DB	9,"ROOM EDITOR: LEVEL %s",0
ROOMI:	DB	9,"ROOM",9,"       %02dX%02d",9,9,"PALETE",9,"%d",10
	DB	9,"HEIGHT 0",9,"%04d",9,9,"SET",9,"%d",10
	DB	9,"HEIGHT 1",9,"%04d",10
	DB	9,"HEIGHT 2",9,"%04d",0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	EDLEVEL,COLORGRID16,GETROOM

DRAWRMATRIX:
	LD	B,ROOMYSIZ
	LD	DE,0

S.LOOPY:PUSH	BC
	PUSH	DE
	LD	B,ROOMXSIZ

S.LOOPX:PUSH	BC
	PUSH	DE
	PUSH	DE
	LD	C,E
	LD	B,D
	LD	DE,(EDLEVEL)
	LD	A,0
	CALL	GETROOM
	POP	DE
	JR	Z,S.ENDX
	LD	A,(HL)
	INC	HL
	OR	(HL)
	CALL	NZ,COLORGRID16

S.ENDX:	POP	DE
	INC	D
	POP	BC
	DJNZ	S.LOOPX

	POP	DE
	INC	E
	POP	BC
	DJNZ	S.LOOPY
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = EVENT
;	DE = SCREEN POSITION

	CSEG
	EXTRN	EDROOM,ED.MAP,GRIDPOS

POSEVENT:
	PUSH	AF
	CALL	GRIDPOS
	POP	AF
	CP	MS_BUTTON1
	JR	NZ,P.1
	LD	(EDROOM),DE
	CALL	ED.MAP
	JP	EDINIT

P.1:	CP	MS_BUTTON2
	RET	NZ
	LD	(EDROOM),DE
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	SETPAL,GETPAL

LEVELPAL:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT PALETE NUMBER
	JR	NZ,P.DEC
	LD	A,(PALETE)
	CP	NR_PALETES-1
	RET	Z
	INC	A
	JR	P.RET

P.DEC:	CP	MS_BUTTON2
	RET	NZ
	LD	A,(PALETE)
	OR	A			;BUTTON 2 DECREMENT PALETE NUMBER
	RET	Z
	DEC	A
P.RET:	LD	IY,(LPTR)
	LD	(PALETE),A
	LD	(IY+LVL.PAL),A
	LD	E,A
	CALL	GETPAL
	JP	SETPAL

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: A = EVENT

	CSEG
	EXTRN	LDPATSET

LEVELSET:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT SET NUMBER
	JR	NZ,S.DEC
	LD	A,(PATSET)
	CP	NR_PATSET-1
	RET	Z
	INC	A
	JR	S.RET

S.DEC:	CP	MS_BUTTON2
	RET	NZ
	LD	A,(PATSET)
	OR	A			;BUTTON 2 DECREMENT SET NUMBER
	RET	Z
	DEC	A
S.RET:	LD	(PATSET),A
	LD	IY,(LPTR)
	LD	(IY+LVL.GFX),A
	RET
	LD	E,A			;THERE IS A SET CHANGE, SO UPDATE
	JP	LDPATSET		;THE GRAPHICS

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	DSEG
LPTR:	DW	0
PALETE:	DB	0
PATSET:	DB	0

