

	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	LEVEL.INC

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = PATTERN STACK
;	BC = SCREEN COORDINATES

	CSEG
	EXTRN	PSTACK

DRAWSTACK:
	BIT	7,B			;CHECK IF THE SCREEN POSITION IS
	RET	NZ			;NEGATIVE
	BIT	7,C
	RET	NZ
	LD	A,NR_SCRCOL		;OR THE X POSITION IS OUTSIDE
	CP	B
	RET	C
	LD	A,NR_SCRROW		;OR THE Y POSITION IS OUTSIDE
	CP	C
	RET	C

	LD	A,C			;Y = YPATTERN*8
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	C,A
	LD	A,B			;X = XPATTERN*16
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	B,A
	JP	PSTACK			;PRINT THE STACK

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = FLOOR MAP NUMBER
;	BC = SCREEN OFFSET
;	A = HEIGHT

	CSEG
	PUBLIC	MAPFLOOR
	EXTRN	CARTPAGE

MAPFLOOR:
	LD	L,A
	LD	A,D			;MAP 0 IS EMPTY MAP, SO RETURN
	OR	E
	RET	Z
	LD	A,C
	SUB	L
	LD	C,A			;CALCULATE INITIAL PATTERN
	PUSH	BC			;SAVE INITIAL PATTERN

	CALL	GETFMAP			;HL = FLOOR MAP ADDRESS
	LD	DE,M.MAP
	LD	BC,LVLXSIZ*LVLYSIZ
	LDIR				;COPY THE MAP TO LOCAL COPY
	LD	E,LEVELPAGE		;SET LEVEL DEFINITION PAGE
	CALL	CARTPAGE

	POP	DE
	LD	HL,M.MAP
	LD	B,LVLYSIZ
M.LOOPY:PUSH	BC			;LOOP OVER X
	PUSH	DE

	LD	B,LVLXSIZ
M.LOOPX:PUSH	BC			;LOOP OVER Y
	PUSH	DE
	PUSH	HL

	LD	A,(HL)
	OR	A
	JR	Z,M.ENDX		;FLOOR 0 MEANS EMPTY FLOOR

	PUSH	DE
	LD	E,A
	CALL	GETFLOOR
	EX	DE,HL			;DE = FLOOR DEFINITION ADDRESS
	POP	BC			;BC = SCREEN POSITION
	LD	A,2

M.LOOPI:PUSH	AF			;LOOP OVER ALL THE ROWS OF THE STACK
	PUSH	DE
	PUSH	BC
	CALL	DRAWSTACK
	POP	BC			;INCREMENT Y
	INC	C
	POP	DE
	LD	HL,NR_LAYERS
	ADD	HL,DE
	EX	DE,HL			;DE POINT TO NEXT ROW
	POP	AF
	DEC	A			;CONTROL NUMBER OF ITERATIONS
	JR	NZ,M.LOOPI

M.ENDX:	POP	HL
	INC	HL
	POP	DE
	INC	D
	INC	E
	POP	BC
	DJNZ	M.LOOPX			;END OF X LOOP

	POP	DE
	DEC	D
	INC	E
	POP	BC
	DJNZ	M.LOOPY			;END OF Y LOOP
	RET

	DSEG
M.MAP:	DS	LVLXSIZ*LVLYSIZ

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = MAP NUMBER
;OUTPUT:A = CARTRIDGE PAGE
;	HL = OFFSET

	CSEG
	PUBLIC	GETFMAP
	EXTRN	CARTPAGE

GETFMAP:DEC	DE			;0 IS THE EMPTY MAP
	LD	A,E			;PAGE = 7BIT-E | D<<1
	RLCA
	LD	A,D
	RLCA
	PUSH	AF
	LD	A,E			;OFFSET = 0-6BIT-E * 128
	AND	7FH
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,CARTSEG
	ADD	HL,DE
	POP	AF
	ADD	A,LEVELPAGE+1
	LD	E,A
	CALL	CARTPAGE
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = MAP NUMBER

	CSEG
	PUBLIC	GETTMAP

GETTMAP:CALL	GETFMAP			;TILE INFOMATION IS LOCATED AFTER
	LD	DE,LVLXSIZ*LVLYSIZ	;THE MAP INFORMATION
	ADD	HL,DE
	RET


