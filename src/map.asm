

	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	LEVEL.INC

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = MAP NUMBER
;	BC = SCREEN OFFSET
;	A = HEIGHT

	CSEG
	PUBLIC	MAP

MAP:	LD	(M.HGHT),A
	LD	(M.OFFS),BC
	LD	(M.MNUM),DE
	LD	A,D
	OR	E
	RET	Z			;0 IS EMPTY MAP, RETURN
	CALL	MAPFLOOR
	JP	MAPTILE

	DSEG
M.MNUM:	DW	0
M.OFFS:	DW	0
M.HGHT:	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = PATTERN STACK
;	BC = SCREEN COORDINATES

	CSEG
	EXTRN	PSTACK

DRAWSTACK:
	BIT	7,B			;CHECK IF THE SCREEN POSITION IS
	RET	NZ			;NEGATIVE
	BIT	7,C
	RET	NZ
	LD	A,NR_SCRCOL		;OR THE X POSITION IS OUTSIDE
	CP	B
	RET	C
	LD	A,NR_SCRROW		;OR THE Y POSITION IS OUTSIDE
	CP	C
	RET	C

	LD	A,C			;Y = YPATTERN*8
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	C,A
	LD	A,B			;X = XPATTERN*16
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	B,A
	JP	PSTACK			;PRINT THE STACK

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = SCREEN POSITION
;	A = FLOOR NUMBER

	CSEG

FLOORFUN:
	PUSH	DE
	LD	E,A
	CALL	GETFLOOR
	EX	DE,HL			;DE = FLOOR DEFINITION ADDRESS
	POP	BC			;BC = SCREEN POSITION
	LD	A,FLOORYSIZ

F.LOOPI:PUSH	AF			;LOOP OVER ALL THE ROWS OF THE STACK
	PUSH	DE
	PUSH	BC
	CALL	DRAWSTACK
	POP	BC			;INCREMENT Y
	INC	C
	POP	DE
	LD	HL,NR_LAYERS
	ADD	HL,DE
	EX	DE,HL			;DE POINT TO NEXT ROW
	POP	AF
	DEC	A			;CONTROL NUMBER OF ITERATIONS
	JR	NZ,F.LOOPI
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = SCREEN POSITION
;	A = FLOOR NUMBER

	CSEG

TILEFUN:PUSH	DE
	LD	E,A
	CALL	GETTILE
	LD	DE,TILE.MAP
	ADD	HL,DE
	EX	DE,HL			;DE = TILE DEFINITION ADDRESS
	POP	BC			;BC = SCREEN POSITION
	LD	A,TILEYSIZ

T.LOOPI:PUSH	AF			;LOOP OVER ALL THE ROWS OF THE STACK
	PUSH	DE
	PUSH	BC
	CALL	DRAWSTACK
	POP	BC			;INCREMENT Y
	INC	C
	POP	DE
	LD	HL,NR_LAYERS
	ADD	HL,DE
	EX	DE,HL			;DE POINT TO NEXT ROW
	POP	AF
	DEC	A			;CONTROL NUMBER OF ITERATIONS
	JR	NZ,T.LOOPI
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	(M.MNUM) = FLOOR MAP NUMBER
;	(M.OFFS) = SCREEN OFFSET
;	(M.HGHT) = HEIGHT

	CSEG
	EXTRN	CARTPAGE

MAPTILE:LD	BC,(M.OFFS)		;CALCULATE INITIAL POSITION
	LD	A,C
	SUB	TILEYSIZ-2
	LD	C,A
	LD	(M.POS),BC
	LD	HL,TILEFUN
	LD	(M.FUN),HL
	LD	DE,(M.MNUM)
	CALL	GETTMAP			;HL = FLOOR MAP ADDRESS
	JR	MAP.AUX

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	(M.MNUM) = FLOOR MAP NUMBER
;	(M.OFFS) = SCREEN OFFSET
;	(M.HGHT) = HEIGHT

	CSEG
	EXTRN	CARTPAGE

MAPFLOOR:
	LD	BC,(M.OFFS)
	LD	(M.POS),BC		;SAVE INITIAL PATTERN
	LD	HL,FLOORFUN
	LD	(M.FUN),HL
	LD	DE,(M.MNUM)
	CALL	GETFMAP			;HL = FLOOR MAP ADDRESS

	;CONTINUE IN MAP.AUX

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	HL = MAP POINTER
;	(M.POS) = INITIAL PATTERN POSITION
;	(M.FUN) = CALLBACK FUNCTION

	CSEG
	EXTRN	PTRCALL

MAP.AUX:LD	DE,M.MAP
	LD	BC,LVLXSIZ*LVLYSIZ
	LDIR				;COPY THE MAP TO LOCAL COPY
	LD	E,LEVELPAGE		;SET LEVEL DEFINITION PAGE
	CALL	CARTPAGE

	LD	DE,(M.POS)		;DE = INITIAL POSITION
	LD	HL,M.MAP
	LD	B,LVLYSIZ
M.LOOPY:PUSH	BC			;LOOP OVER X
	PUSH	DE

	LD	B,LVLXSIZ
M.LOOPX:PUSH	BC			;LOOP OVER Y
	PUSH	DE
	PUSH	HL

	LD	A,(HL)
	OR	A			;0 MEANS EMPTY ELEMENT
	LD	HL,(M.FUN)
	CALL	NZ,PTRCALL

M.ENDX:	POP	HL
	INC	HL
	POP	DE
	INC	D
	INC	E
	POP	BC
	DJNZ	M.LOOPX			;END OF X LOOP

	POP	DE
	DEC	D
	INC	E
	POP	BC
	DJNZ	M.LOOPY			;END OF Y LOOP
	RET

	DSEG
M.MAP:	DS	LVLXSIZ*LVLYSIZ
M.POS:	DW	0
M.FUN:	DW	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = MAP NUMBER
;OUTPUT:A = CARTRIDGE PAGE
;	HL = OFFSET

	CSEG
	PUBLIC	GETFMAP
	EXTRN	CARTPAGE

GETFMAP:DEC	DE			;0 IS THE EMPTY MAP
	LD	A,E			;PAGE = 7BIT-E | D<<1
	RLCA
	LD	A,D
	RLCA
	PUSH	AF
	LD	A,E			;OFFSET = 0-6BIT-E * 128
	AND	7FH
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,CARTSEG
	ADD	HL,DE
	POP	AF
	ADD	A,LEVELPAGE+1
	LD	E,A
	CALL	CARTPAGE
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	DE = MAP NUMBER

	CSEG
	PUBLIC	GETTMAP

GETTMAP:CALL	GETFMAP			;TILE INFOMATION IS LOCATED AFTER
	LD	DE,LVLXSIZ*LVLYSIZ	;THE MAP INFORMATION
	ADD	HL,DE
	RET


