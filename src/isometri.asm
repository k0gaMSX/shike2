	INCLUDE	SHIKE2.INC
	INCLUDE	BIOS.INC


MASKY		EQU	128		;Y COORDENATE OF BITMAP MASKS
MASKTMP		EQU	00D4H		;XY COORDENATE FOR MASK OPERATIONS
MASKLEFTDW	EQU	3		;MASK LEFT DOWN
MASKRGHTDW	EQU	2		;MASK RIGTH DOWN
MASKLEFTUP	EQU	1		;MASK LEFT UP
MASKRGHTUP	EQU	0		;MASK RIGTH UP
MASKUP		EQU	4		;MASK UP
MASKDOWN	EQU	5		;MASK DOWN
MASKLEFT	EQU	6		;MASK LEFT
MASKRIGTH	EQU	7		;MASK RIGHT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:		DE = TILE

	CSEG

ISVISIBLE:
	LD	A,E
	CP	NR_SCRROW
	JR	NC,V.NO

	LD	A,D
	CP	NR_SCRCOL
	JR	NC,V.NO
	LD	A,1
	OR	A
	RET				; Z = 0, NO VISIBLE

V.NO:	XOR	A			; Z = 1, VISIBLE
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:		DE = INTIAL TILE POSITION
;		HL = CALLBACK FUNCTION
;		B = NUMBER OF ITERATIONS (NUMBER OF 'DOTS' IN THE LINE)
;		(ISODIR) = 0 -> POSITIVE (RIGTH/DOWN)
;			   1 -> NEGATIVE (LEFT/UP)
	CSEG

ISOLINEV:
	LD	A,(ISODIR)
	OR	A
	JR	NZ,L.VNEG
	LD	A,1			;POSITIVE INCREMENT
	LD	(I.INCY),A
	LD	A,1
	LD	(I.INCX),A
	JR	I.TEST

L.VNEG:	LD	A,-1			;NEGATIVE INCREMENT
	LD	(I.INCY),A
	LD	A,-1
	LD	(I.INCX),A
	JR	I.TEST

ISOLINEH:
	LD	A,(ISODIR)
	OR	A
	JR	NZ,L.HNEG
	LD	A,-1			;POSITIVE INCREMENT
	LD	(I.INCY),A
	LD	A,1
	LD	(I.INCX),A
	JR	I.TEST

L.HNEG:	LD	A,1			;NEGATIVE INCREMENT
	LD	(I.INCY),A
	LD	A,-1
	LD	(I.INCX),A

I.TEST:	LD	A,B			;IF THE SIZE IS 0, THEN RETURN
	OR	B
	RET	Z

I.LOOP:	PUSH	BC			;LOOP OVER ISO X DRAWING AN
	PUSH	DE			;ISOMETRIC LINE
	PUSH	HL

	LD	BC,L.RET
	PUSH	BC
	JP	(HL)			;GO TO CALLBACK FUNCTION

L.RET:	POP	HL
	POP	DE
	POP	BC

	LD	A,(I.INCX)
	ADD	A,D
	LD	D,A
	LD	A,(I.INCY)
	ADD	A,E
	LD	E,A
	DJNZ	I.LOOP
	RET

	DSEG
I.INCX:	DB	0
I.INCY:	DB	0
ISODIR:	DB	0


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:		(TILEINC) = UPPER -> X INCREMENT-1, LOWER -> Y INCREMENT-1
;		(TILE) = INITIAL TILE
;		(PATTERN) = ACTUAL PATTERN
;		(ISODIR) = 0 -> POSITIVE (RIGTH), 1 -> NEGATIVE (LEFT)
;		(ACPAGE) = PAGE

	CSEG
	PUBLIC	DRAWREGION
	EXTRN	VDPPAGE

DRAWREGION:
	LD	A,DEFARG
	LD	(CMDARG),A
	LD	A,(ISODIR)
	OR	A
	JR	Z,R.POSITIVE

R.NEGATIVE:
	LD	DE,(TILE)
	LD	(R.CORNERL),DE		;CALCULATE LEFT CORNER
	LD	BC,(TILEINC)

	LD	A,B
	OR	A
	JR	Z,R.NU2
R.NU1:	DEC	D
	INC	E
	DJNZ	R.NU1
R.NU2:	LD	(R.CORNERU),DE		;CALCULATE UPPER RIGTH CORNER

	LD	DE,(TILE)
	LD	B,C
	LD	A,B
	OR	A
	JR	Z,R.ND2
R.ND1:	DEC	D
	DEC	E
	DJNZ	R.ND1
R.ND2:	LD	(R.CORNERD),DE		;CALCULATE LOWER LEFT CORNER
	JR	R.BODY


R.POSITIVE:
	LD	DE,(TILE)
	LD	(R.CORNERL),DE		;CALCULATE LEFT CORNER
	LD	BC,(TILEINC)

	LD	A,B
	OR	A
	JR	Z,R.U2
R.U1:	INC	D
	DEC	E
	DJNZ	R.U1
R.U2:	LD	(R.CORNERU),DE		;CALCULATE UPPER RIGTH CORNER

	LD	DE,(TILE)
	LD	B,C
	LD	A,B
	OR	A
	JR	Z,R.D2
R.D1:	INC	D
	INC	E
	DJNZ	R.D1
R.D2:	LD	(R.CORNERD),DE		;CALCULATE LOWER LEFT CORNER


R.BODY:	LD	BC,(TILEINC)
	LD	A,C
	OR	A
	JR	NZ,R.NHOR
	;
	;	   06
	;	  16
	;	 16
	;	86
	;
	CALL	R.TILE8
	CALL	R.TILES1
	CALL	R.TILE0
	JP	R.TILES6

R.NHOR:	LD	A,B
	OR	A
	JR	NZ,R.NVER
	;
	;	83
	;	 43
	;	  43
	;	   49
	LD	DE,(R.CORNERL)
	DEC	E
	LD	(R.CORNERU),DE
	CALL	R.TILE8
	CALL	R.TILES3
	CALL	R.TILES4
	JP	R.TILE9

R.NVER:
	;
	;          0
	;         173
	;        17773
	;       8777723
	;        47726
	;         426
	;          4
	;
	CALL	R.TILE8
	CALL	R.TILES1
	CALL	R.TILE0
	CALL	R.TILES3
	CALL	R.TILES4
	CALL	R.TILES7
	CALL	R.TILES2
	JP	R.TILES6

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
R.TILE0:
	LD	DE,(R.CORNERU)
	LD	A,MASKUP
	LD	(MASK),A
	JP	DRAWTILEM

R.TILES1:
	LD	BC,(TILEINC)
	DEC	B
	LD	DE,(R.CORNERL)
	INC	D
	DEC	E
	LD	HL,DRAWTILEM
	LD	A,MASKLEFTUP
	LD	(MASK),A
	JP	ISOLINEH

R.TILES2:
	LD	BC,(TILEINC)
	DEC	B
	LD	DE,(R.CORNERD)
	INC	D
	DEC	E
	LD	HL,DRAWTILE
	JP	ISOLINEH

R.TILES3:
	LD	DE,(R.CORNERU)
	INC	E
	INC	D
	LD	HL,DRAWTILEM
	LD	BC,(TILEINC-1)
	LD	A,MASKRGHTUP
	LD	(MASK),A
	JP	ISOLINEV

R.TILES4:
	LD	DE,(R.CORNERL)
	INC	E
	INC	D
	LD	HL,DRAWTILEM
	LD	BC,(TILEINC-1)
	LD	A,MASKLEFTDW
	LD	(MASK),A
	JP	ISOLINEV

R.TILE5:
	LD	DE,(R.CORNERU)
	LD	A,MASKDOWN
	LD	(MASK),A
	JP	DRAWTILEM

R.TILES6:
	LD	BC,(TILEINC)
	LD	DE,(R.CORNERD)
	INC	D
	LD	HL,DRAWTILEM
	LD	A,MASKRGHTDW
	LD	(MASK),A
	JP	ISOLINEH


R.TILES7:
	LD	DE,(R.CORNERL)
	INC	D
	LD	BC,(TILEINC)
	DEC	C
	DEC	B
	LD	HL,DRAWTILE
	JP	ISORECT

R.TILE8:
	LD	DE,(R.CORNERL)
	LD	A,MASKLEFT
	LD	(MASK),A
	JP	DRAWTILEM

R.TILE9:
	LD	DE,(R.CORNERD)
	INC	D
	LD	A,MASKRIGTH
	LD	(MASK),A
	JP	DRAWTILEM

	DSEG
R.CORNERU:	DW	0
R.CORNERD:	DW	0
R.CORNERL:	DW	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:		(TILEINC) = UPPER -> X INCREMENT-1, LOWER -> Y INCREMENT-1
;		(TILE) = INITIAL TILE
;		(ISODIR) = 0 -> POSITIVE (RIGTH), 1 -> NEGATIVE (LEFT)
;		(ACPAGE) = PAGE

	CSEG
	PUBLIC	MARKREGION

MARKREGION:
	LD	A,DEFARG
	LD	(CMDARG),A
	LD	BC,(TILEINC)
	LD	HL,MARKTILE
	LD	DE,(TILE)
	;CONTINUE IN ISORECT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:		HL = CALLBACK FUNCTION
;		B = X INC (MINUS ONE)
;		C = Y INC (MINUS ONE)
;		DE = INITIAL TILE
;		(ISODIR) = 0 -> POSITIVE (RIGTH), 1 -> NEGATIVE (LEFT)
;
;WE HAVE TO PAINT LINES PARAREL TO THE SIDES, WITH THE SAME
;SIZE THAN THE SIDES, BUT ALSO WE HAVE TO PAINT LINES WITH DIFFERENT
;SIZE. TAKE A LOOK OF THE DRAW:
;         L
;        LOL
;	LOLOL
;        LOLOL
;         LOL
;          L
;SO THE FIRST CALL TO R.HELPER DRAWS THE L PATTERNS, WHILE SECOND CALL
;DRAWS THE O PATTERNS. YOU CAN SEE THAT THE SECOND CALL IS ONE PATTERN
;LESS IN X AND Y.

	CSEG
ISORECT:
	PUSH	DE
	PUSH	BC
	CALL	R.HELPER
	POP	BC
	POP	DE
	DEC	C
	DEC	B
	INC	D
	;CONTINUE IN HELPER

;INPUT:		HL = CALLBACK FUNCTION
;		B = X INC (MINUS ONE)
;		C = Y INC (MINUS ONE)
;		DE = INITIAL TILE
;		(ISODIR) = 0 -> POSITIVE (RIGTH), 1 -> NEGATIVE (LEFT)

R.HELPER:
	INC	C
	RET	Z			;RETURN BECAUSE Y=0
	INC	B
	RET	Z			;RETURN BECAUSE X=0

R.LOOP:	PUSH	BC
	PUSH	DE			;LOOP OVER Y AND DRAW PARAREL ISO LINES
	CALL	ISOLINEH
	POP	DE
	INC	E
	INC	D
	POP	BC
	DEC	C
	JR	NZ,R.LOOP
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:		D = X TILE POSITION
;		E = Y TILE POSITION


	CSEG
        PUBLIC	TILE2XY

TILE2XY:
	LD	A,E			;CONVERT FROM TILE SPACE
	ADD	A,A			;TO SCREEN COORDINATES
	ADD	A,A
	ADD	A,A
	LD	E,A

	LD	A,D
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	D,A
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:		DE = TILE POSITION WE WANT MARK
;		(PATTERN) = PATTERN NUMBER
;		(ACPAGE) = PAGE
;		(CMDARG) = VDP COMMAND ARGUMENT


	CSEG
	EXTRN	HMMM,VDPPAGE

DRAWTILE:
	CALL	ISVISIBLE
	RET	Z
	CALL	TILE2XY
	PUSH	DE
	LD	DE,(PATTERN)
	CALL	TILE2XY
	LD	L,E
	LD	H,D
	POP	DE
	LD	BC,1008H
	LD	A,PATPAGE
	LD	(VDPPAGE),A
	JP	HMMM

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:		DE = TILE POSITION WE WANT MARK
;		(PATTERN) = PATTERN NUMBER
;		(MASK) = MASK USED IN THE PATTERN
;		(ACPAGE) = PAGE
;		(CMDARG) = VDP COMMAND ARGUMENT

	CSEG
	EXTRN	LMMM,HMMM,VDPPAGE
DRAWTILEM:
	CALL	ISVISIBLE
	RET	Z
	PUSH	DE
	LD	A,PATPAGE
	LD	(VDPPAGE),A
	LD	A,(MASK)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	H,A
	LD	L,MASKY
	LD	DE,MASKTMP
	LD	BC,1008H
	CALL	HMMM			;COPY THE MASK TO THE TEMPORAL SPACE

	LD	DE,(PATTERN)		;TODO: MAYBE THIS SHOULD BE PATTERN NUM
	CALL	TILE2XY
	LD	A,LOGAND
	LD	(LOGOP),A
	LD	L,E
	LD	H,D
	LD	DE,MASKTMP
	LD	BC,1008H
	CALL	LMMM			;COPY THE PATTERN WITH AND OPERATION

	POP	DE
	CALL	TILE2XY
	LD	HL,MASKTMP
	LD	BC,1008H
	LD	A,(ACPAGE)
	LD	(VDPPAGE),A
	LD	A,LOGTIMP
	LD	(LOGOP),A
	JP	LMMM			;COPY THE RESULT TO THE SCREEN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:		DE = TILE POSITION WE WANT MARK
;		(ACPAGE) = PAGE
;		(CMDARG) = VDP COMMAND ARGUMENT

	CSEG
	EXTRN	LINE

MARKTILE:
	CALL	ISVISIBLE
	RET	Z
	CALL	TILE2XY			;TRANSFORM IT TO COORDINATES

	LD	A,LOGXOR
	LD	(LOGOP),A
	LD	A,MARKCOLOR
	LD	(FORCLR),A

	LD	A,D
	ADD	A,15
	LD	B,A
	LD	C,E
	PUSH	BC
	CALL	LINE			;LINE ORIGIN-RIGTH

	POP	DE
	LD	A,E
	ADD	A,7
	LD	C,A
	LD	B,D
	PUSH	BC
	CALL	LINE			;LINE RIGTH-UP/RIGTH

	POP	DE
	LD	A,D
	ADD	A,-15
	LD	B,A
	LD	C,E
	PUSH	BC
	CALL	LINE			;LINE UP/RIGTH-UP/LEFT

	POP	DE
	LD	A,E
	ADD	A,-7
	LD	C,A
	LD	B,D
	JP	LINE			;LINE UP/LEFT-ORIGIN



	DSEG
	PUBLIC	TILE,TILEINC,PATTERN
MASK:		DW	0		;ACTUAL TILE MASK. PARAMETER BY DEFAULT
TILE:		DW	0		;ACTUAL TILE. PARAMETER BY DEFAULT
PATTERN:	DW	0		;ACTUAL PATTERN. PARAMETER BY DEFAULT
TILEINC:	DW	0		;ACTUAL INCREMENT. PARAMETER BY DEFAULT



