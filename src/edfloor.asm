
	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	LEVEL.INC
	INCLUDE	EVENT.INC

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	EDFLOOR
	EXTRN	EDINIT,MPRESS,VDPSYNC,CARTPAGE,LISTEN

EDFLOOR:CALL	EDINIT			;COMMON INITIALIZATION

ED.LOOP:LD	E,LEVELPAGE		;PUTS DATA OF LEVELS
	CALL	CARTPAGE
	CALL	GETFDATA		;CALCULATE NUMPAT1 AND NUMPAT2
	CALL	SHOWSCR			;PAINT THE SCREEN
	CALL	VDPSYNC			;WAIT TO THE VDP
	LD	DE,RECEIVERS
	CALL	LISTEN			;WAIT AN EVENT
	JR	NZ,ED.LOOP
	RET

	EXTRN	PALEVENT,SETEVENT

RECEIVERS:
	DB	80,30,30,8
	DW	CHANGEFLOOR
	DB	80,30,38,8
	DW	PALEVENT
	DB	80,30,46,8
	DW	SETEVENT
	DB	80,16,78,8
	DW	PUTPATTERN1
	DB	80,16,86,8
	DW	PUTPATTERN2
	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	GETNUMPAT

GETFDATA:
	LD	A,(FLOOR)
	LD	E,A
	CALL	GETFLOOR		;GET THE POINTER TO THE FLOOR
	LD	(FPTR),HL		;SAVE FLOOR POINTER

	PUSH	HL
	CALL	GETNUMPAT		;CALCULATE NUMPAT1
	LD	(NUMPAT1),A
	POP	HL
	LD	DE,NR_LAYERS
	ADD	HL,DE
	CALL	GETNUMPAT
	LD	(NUMPAT2),A		;CALCULATE NUMPAT2
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	HMMV

CLEANSTACK:
	LD	DE,80*256 + 78
	LD	BC,16*256 + FLOORYSIZ
	XOR	A
	LD	(FORCLR),A
	JP	HMMV			;BLACK RECTANGLE



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


	CSEG
	EXTRN	EDSET,EDPAL,DRAWSTACKS,LOCATE,PRINTF,GLINES

SHOWSCR:CALL	CLEANSTACK		;CLEAN THE PATTERN STACK VISUALIZATION

	LD	DE,0
	CALL	LOCATE			;LOCATE THE CURSOR IN THE BEGINNING
	LD	H,0			;OF SCREEN
	LD	A,(NUMPAT2)
	LD	L,A
	PUSH	HL
	LD	A,(NUMPAT1)
	LD	L,A
	PUSH	HL
	LD	A,(EDSET)
	LD	L,A
	PUSH	HL
	LD	A,(EDPAL)
	LD	L,A
	PUSH	HL
	LD	A,(FLOOR)
	LD	L,A
	PUSH	HL
	LD	DE,FMT
	CALL	PRINTF			;PRINT THE TEXT INFORMATION

	LD	DE,FLOORG
	LD	C,15
	CALL	GLINES			;DRAW ALL THE SCREEN LINES

	LD	HL,(FPTR)
	LD	E,FLOORYSIZ
	LD	BC,80*256 + 78
	JP	DRAWSTACKS


;	       REP  X0  Y0    X1  Y1 IX0 IY0 IX1 IY1
FLOORG:	DB	4,  80, 30,  110, 30,  0,  8,  0,  8
	DB	2,  80, 30,   80, 54, 30,  0, 30,  0
	DB	3,  80, 78,   96, 78,  0,  8,  0,  8
	DB	2,  80, 78,   80, 94, 16,  0, 16,  0
	DB	0

FMT:	DB	10,10,10,10
	DB	9,9,"     FLOOR",9,"%03d",10
	DB	9,9,"     PALETE",9,"%3d",10
	DB	9,9,"     SET",9,"%3d",10
	DB	10,10,10
	DB	9,9,9," %d",10
	DB	9,9,9," %d",0



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;A = EVENT

	CSEG
	EXTRN	PATEVENT

PUTPATTERN1:
	LD	BC,(NUMPAT1)
	LD	HL,(FPTR)
	LD	DE,0
	JP	PATEVENT

PUTPATTERN2:
	LD	BC,(NUMPAT2)
	LD	HL,(FPTR)
	LD	DE,NR_LAYERS
	JP	PATEVENT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = EVENT

	CSEG

CHANGEFLOOR:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT FLOOR NUMBER
	LD	A,(FLOOR)
	JR	NZ,F.DEC
	CP	NR_FLOORS-1
	RET	Z
	INC	A
	JR	F.RET

F.DEC:	OR	A
	RET	Z
	DEC	A			;BUTTON 2 DECREMENT FLOOR NUMBER
F.RET:	LD	(FLOOR),A

	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	DSEG
FPTR:	DW	0
FLOOR:	DB	0
NUMPAT1:DB	0
NUMPAT2:DB	0

