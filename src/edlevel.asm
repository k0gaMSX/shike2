
	INCLUDE	SHIKE2.INC
	INCLUDE	BIOS.INC
	INCLUDE	EDITOR.INC
	INCLUDE	KBD.INC
	INCLUDE	VDP.INC

PTRSPR		EQU	0

PTRPAT		EQU	LASTPAT

NR_LEVELS	EQU	3
NR_MAPS		EQU	8
LISTCOORD	EQU	00003H
MAPCOORD	EQU	03080H



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	EDLEVEL
	EXTRN	VDPSYNC,EDINIT,INITLEVELS

EDLEVEL:CALL	EDINIT
	CALL	INITLEVELS
	CALL	INITSPRITES
	CALL	LEVELSCR
	CALL	VDPSYNC
	CALL	SELLEVEL
	CALL	DRAWLEVEL
	CALL	VDPSYNC
	CALL	GETCHAR
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	PUTSPRITE,GETCHAR,MOVEUC,KEY2DIR

SELLEVEL:
	LD	A,(LEVEL)
	LD	DE,LISTCOORD
	ADD	A,E
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	E,A
	LD	D,4*15			;2*TAB-1
	LD	BC,PTRPAT*256 + PTRSPR
	CALL	PUTSPRITE
	CALL	GETCHAR

	CP	KB_SPACE
	RET	Z

	CALL	KEY2DIR
	JR	C,SELLEVEL
	LD	DE,(LEVEL)
	CALL	MOVEUC
	LD	A,E
	CP	-1
	JR	Z,SELLEVEL
	CP	NR_LEVELS
	JR	Z,SELLEVEL
	LD	(LEVEL),A
	JR	SELLEVEL

	DSEG

LEVEL:	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	(LEVEL) = ACTUAL LEVEL SELECTED

	CSEG
	EXTRN	LEVELS,MULTEA,LINE

DRAWLEVEL:
	LD	A,LOGIMP
	LD	(LOGOP),A
	LD	A,(LEVEL)		;ADRESS THE LEVEL
	LD	E,LEVEL.SIZ
	CALL	MULTEA
	EX	DE,HL			;DE = (LEVEL) * LEVEL.SIZ
	LD	IX,LEVELS
	ADD	IX,DE			;IX = LEVELS + LVLOFFSET
	CALL	DRAWMAPS

	LD	A,LVLCOLOR
	LD	(FORCLR),A
	LD	DE,MAPCOORD
	LD	A,(IX+LEVEL.YSIZ)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,E
	LD	C,A
	LD	B,D
	LD	HL,0800H
	LD	A,(IX+LEVEL.XSIZ)
	INC	A
	CALL	GLINES			;DRAW VERTICAL LINES
	
	LD	DE,MAPCOORD
	LD	A,(IX+LEVEL.XSIZ)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,D
	LD	B,A
	LD	C,E
	LD	HL,0008H
	LD	A,(IX+LEVEL.YSIZ)
	INC	A
	CALL	GLINES			;DRAW HORIZONTAL LINES
	RET


GLINES:	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	LINE
	POP	HL

	POP	DE			;INCREMENT ORIGIN POINT
	LD	A,D
	ADD	A,H
	LD	D,A
	LD	A,E
	ADD	A,L
	LD	E,A

	POP	BC			;INCREMENT DESTINE POINT
	LD	A,B
	ADD	A,H
	LD	B,A
	LD	A,C
	ADD	A,L
	LD	C,A

	POP	AF
	DEC	A
	JR	NZ,GLINES
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	IX = POINTER TO THE LEVEL

	CSEG
	EXTRN	GETMAP,HMMV

DRAWMAPS:
	LD	A,LOGIMP
	LD	(LOGOP),A
	LD	B,(IX+LEVEL.XSIZ)
	LD	C,(IX+LEVEL.YSIZ)
	LD	DE,0

D.Y:	PUSH	BC
	PUSH	DE

D.X:	PUSH	BC
	PUSH	DE
	LD	A,D
	RLCA
	RLCA
	RLCA
	LD	B,A
	LD	A,E
	RLCA
	RLCA
	RLCA
	LD	C,A
	LD	HL,MAPCOORD
	ADD	HL,BC

	PUSH	HL
	CALL	GETMAP
	JR	Z,D.NOMAP
	LD	A,MAPCOLOR
	JR	D.SET
D.NOMAP:LD	A,MAPNOCOLOR
D.SET:	LD	(FORCLR),A
	POP	DE

	LD	BC,0808H
	CALL	HMMV
	POP	DE
	INC	D
	POP	BC
	DJNZ	D.X

	POP	DE
	INC	E
	POP	BC
	DEC	C
	JR	NZ,D.Y
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	SPRITE,SETCOLSPR,DELSPR
	

INITSPRITES:
	CALL	DELSPR
	LD	BC,1*256 + PTRPAT
	LD	DE,ED.SPRITES
	JP	SPRITE

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	LEVELS,LOCATE,PRINTF
	EXTRN	DISSPR,ENASPR,DISSCR,ENASCR,CLRVPAGE,SETPAGE

LEVELSCR:
	CALL	DISSPR
	CALL	DISSCR
	LD	A,LEVPAGE
	LD	(ACPAGE),A
	LD	(DPPAGE),A
	CALL	SETPAGE			;SET THE CORRECT PAGE
	LD	E,LEVPAGE
	CALL	CLRVPAGE		;CLEAN THE PAGE

	LD	DE,LISTCOORD
	CALL	LOCATE
	LD	B,NR_LEVELS
	LD	IX,LEVELS

L.LOOP:	PUSH	BC
	LD	L,(IX+LEVEL.YSIZ)
	PUSH	HL
	LD	L,(IX+LEVEL.XSIZ)
	PUSH	HL
	LD	L,(IX+LEVEL.NAME)
	LD	H,(IX+LEVEL.NAME+1)
	PUSH	HL
	LD	DE,L.FMT
	CALL	PRINTF
	LD	DE,LEVEL.SIZ
	ADD	IX,DE
	POP	BC
	DJNZ	L.LOOP

	CALL	ENASCR
	CALL	ENASPR
	RET

L.FMT:	9,9,"%s (%dX%d)",10,0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

ED.SPRITES:
	DB	080H,0C0H,0F0H,0F0H,0C0H,080H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H

