
	INCLUDE	SHIKE2.INC
	INCLUDE	BIOS.INC
	INCLUDE	EDITOR.INC
	INCLUDE	KBD.INC
	INCLUDE	VDP.INC

PTRSPR		EQU	0
MAPSPR		EQU	1

PTRPAT		EQU	LASTPAT
MAPPAT		EQU	PTRPAT+4

LISTCOORD	EQU	00003H
MAPCOORD	EQU	03050H
TEXTCOORD	EQU	03010H


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	EDLEVEL
	EXTRN	VDPSYNC,EDINIT

EDLEVEL:CALL	EDINIT
	CALL	INITSPRITES
	CALL	LEVELSCR

	CALL	SELLEVEL
	CALL	SELROOM
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = ROOM POSITION
;OUTPUT:HL = SCREEN COORDINATES

	CSEG

ROOM2XY:LD	A,D
	RLCA
	RLCA
	RLCA
	LD	D,A
	LD	A,E
	RLCA
	RLCA
	RLCA
	LD	E,A
	LD	HL,MAPCOORD
	ADD	HL,DE
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: DE = LOCATION OF THE MAP IN THE LEVEL
;OUTPUT:HL = ADRESS OF THE ROOM IN THE LEVEL

	CSEG
	EXTRN	MULTEA

ROOMADDR:
	PUSH	DE
	LD	D,0
	LD	A,(LVLYSIZ)
	CALL	MULTEA
	POP	DE
	LD	E,D
	LD	D,0
	ADD	HL,DE
	LD	DE,LVLROOM
	ADD	HL,DE
	RET

GETROOM:CALL	ROOMADDR
	LD	A,(HL)
	CP	-1
	RET

SETROOM:PUSH	AF
	CALL	ROOMADDR
	POP	AF
	LD	(HL),A
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	KPRESS,PUTSPRITE,KEY2DIR,MOVEUC,PRINTF,VDPSYNC

SELROOM:LD	DE,0
	JR	M.PRINT			;PRINT MAP NUMBER OF 0,0

M.LOOP:	LD	DE,(S.ROOM)
	CALL	ROOM2XY
	EX	DE,HL
	LD	C,MAPSPR
	LD	B,MAPPAT
	CALL	PUTSPRITE		;MARK THE MAP
	CALL	KPRESS

	CP	KB_SPACE		;SPACE SELECTS THE MAPS
	RET	Z

	CALL	KEY2DIR
	JR	C,M.LOOP
	LD	DE,(S.ROOM)
	CALL	MOVEUC

	LD	A,-1			;CHECK LIMITS
	CP	D
	JR	Z,M.LOOP
	CP	E
	JR	Z,M.LOOP
	LD	A,(LVLXSIZ)
	CP	D
	JR	Z,M.LOOP
	LD	A,(LVLYSIZ)
	CP	E
	JR	Z,M.LOOP

M.PRINT:LD	(S.ROOM),DE
	CALL	GETROOM
	LD	E,A
	PUSH	DE			;PUSH THE ROOM NUMBER
	LD	DE,TEXTCOORD
	CALL	LOCATE
	LD	DE,M.FMT		;PRINT THE ROOM NUMBER
	CALL	PRINTF
	CALL	VDPSYNC
	JR	M.LOOP

M.FMT:	DB	"ROOM: %d",0

	DSEG
S.ROOM:	DW	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	LEVEL,PUTSPRITE,KPRESS,MOVEUC,KEY2DIR
	PUBLIC	SELLEVEL

SELLEVEL:
	LD	A,(S.LEVEL)
	CALL	LEVEL
	LD	A,(S.LEVEL)
	LD	DE,LISTCOORD
	ADD	A,E
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	E,A
	LD	D,4*15			;2*TAB-1
	LD	BC,PTRPAT*256 + PTRSPR
	CALL	PUTSPRITE
	CALL	KPRESS

L.SPACE:CP	KB_SPACE		;SPACE SELECTS THE LEVEL
	JR	NZ,L.DIR
	CALL	DRAWLEVEL		;DRAW THE LEVEL
	CALL	VDPSYNC			;WAIT TO THE VDP
	RET

L.DIR:	CALL	KEY2DIR
	JR	C,SELLEVEL
	LD	DE,(S.LEVEL)
	CALL	MOVEUC

	LD	A,E			;CHECK LIMITS
	CP	-1
	JR	Z,SELLEVEL
	CP	NR_LEVELS
	JR	Z,SELLEVEL
	LD	(S.LEVEL),A
	JR	SELLEVEL

	DSEG

S.LEVEL:	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	LINE

DRAWLEVEL:
	LD	A,LOGIMP
	LD	(LOGOP),A

	LD	A,LVLCOLOR
	LD	(FORCLR),A
	LD	DE,MAPCOORD
	LD	A,(LVLYSIZ)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,E
	LD	C,A
	LD	B,D
	LD	HL,0800H
	LD	A,(LVLXSIZ)
	INC	A
	CALL	GLINES			;DRAW VERTICAL LINES
	
	LD	DE,MAPCOORD
	LD	A,(LVLXSIZ)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,D
	LD	B,A
	LD	C,E
	LD	HL,0008H
	LD	A,(LVLYSIZ)
	INC	A
	CALL	GLINES			;DRAW HORIZONTAL LINES
	CALL	DRAWROOMS
	RET


GLINES:	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	LINE
	POP	HL

	POP	DE			;INCREMENT ORIGIN POINT
	LD	A,D
	ADD	A,H
	LD	D,A
	LD	A,E
	ADD	A,L
	LD	E,A

	POP	BC			;INCREMENT DESTINE POINT
	LD	A,B
	ADD	A,H
	LD	B,A
	LD	A,C
	ADD	A,L
	LD	C,A

	POP	AF
	DEC	A
	JR	NZ,GLINES
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	IX = POINTER TO THE LEVEL

	CSEG
	EXTRN	LMMV

DRAWROOMS:
	LD	A,LOGIMP
	LD	(LOGOP),A
	LD	A,(LVLXSIZ)
	LD	B,A
	LD	A,(LVLYSIZ)
	LD	C,A
	LD	DE,0

D.Y:	PUSH	BC			;LOOP OVER Y
	PUSH	DE

D.X:	PUSH	BC			;LOOP OVER X
	PUSH	DE

	PUSH	DE
	CALL	GETROOM			;TAKE THE COLOR OF THIS ROOM
	JR	Z,D.NOMAP
	LD	A,MAPCOLOR
	JR	D.SET
D.NOMAP:LD	A,MAPNOCOLOR
D.SET:	LD	(FORCLR),A
	POP	DE

	CALL	ROOM2XY
	EX	DE,HL
	INC	D
	INC	E
	LD	BC,0707H
	CALL	LMMV			;PAINT THE RECTANGLE
	POP	DE			;NEXT X ITERATION
	INC	D
	POP	BC
	DJNZ	D.X

	POP	DE			;NEXT Y ITERATION
	INC	E
	POP	BC
	DEC	C
	JR	NZ,D.Y
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	SPRITE,SETCOLSPR,DELSPR
	

INITSPRITES:
	CALL	DELSPR
	LD	BC,2*256 + PTRPAT
	LD	DE,ED.SPRITES
	JP	SPRITE

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	LEVEL,LOCATE,PRINTF
	EXTRN	DISSPR,ENASPR,DISSCR,ENASCR,CLRVPAGE,SETPAGE

LEVELSCR:
	CALL	DISSPR
	CALL	DISSCR
	LD	A,LEVPAGE
	LD	(ACPAGE),A
	LD	(DPPAGE),A
	CALL	SETPAGE			;SET THE CORRECT PAGE
	LD	E,LEVPAGE
	CALL	CLRVPAGE		;CLEAN THE PAGE

	LD	DE,LISTCOORD
	CALL	LOCATE
	XOR	A
	LD	(S.CONT),A

L.LOOP:	CALL	LEVEL			;PRINT ALL THE LEVEL NAMES
	LD	A,(LVLYSIZ)
	LD	L,A
	PUSH	HL
	LD	A,(LVLXSIZ)
	LD	L,A
	PUSH	HL
	LD	HL,LVLNAME
	PUSH	HL
	LD	DE,L.FMT
	CALL	PRINTF
	LD	HL,S.CONT
	LD	A,(HL)
	INC	A
	LD	(HL),A
	CP	NR_LEVELS
	JR	NZ,L.LOOP

	CALL	VDPSYNC
	CALL	ENASCR
	CALL	ENASPR
	RET

L.FMT:	9,9,"%s (%dX%d)",10,0

	DSEG
S.CONT:	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG

ED.SPRITES:
PTRGEN:	DB	080H,0C0H,0F0H,0F0H,0C0H,080H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H

MAPGEN:	DB	0FFH,081H,081H,081H,081H,081H,081H,0FFH
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H



