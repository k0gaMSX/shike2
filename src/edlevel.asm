
	INCLUDE	SHIKE2.INC
	INCLUDE	BIOS.INC
	INCLUDE	EDITOR.INC
	INCLUDE	KBD.INC

PTRSPR		EQU	0

PTRPAT		EQU	LASTPAT

NR_LEVELS	EQU	3
NR_MAPS		EQU	8
LISTCOORD	EQU	0003H

LEVEL.XSIZ	EQU	0
LEVEL.YSIZ	EQU	1
LEVEL.NAME	EQU	2
LEVEL.SIZ	EQU	4


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	EDLEVEL
	EXTRN	VDPSYNC,EDINIT

EDLEVEL:CALL	EDINIT
	CALL	INITSPRITES
	CALL	LEVELSCR
	CALL	VDPSYNC
	CALL	SELLEVEL
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	PUTSPRITE,GETCHAR,MOVEUC,KEY2DIR

SELLEVEL:
	LD	A,(LEVEL)
	LD	DE,LISTCOORD
	ADD	A,E
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	E,A
	LD	D,4*15			;2*TAB-1
	LD	BC,PTRPAT*256 + PTRSPR
	CALL	PUTSPRITE
	CALL	GETCHAR

	CP	KB_ESC
	RET	Z

	CALL	KEY2DIR
	JR	C,SELLEVEL
	LD	DE,(LEVEL)
	CALL	MOVEUC
	LD	A,E
	CP	-1
	JR	Z,SELLEVEL
	CP	NR_LEVELS
	JR	Z,SELLEVEL
	LD	(LEVEL),A
	JR	SELLEVEL

	DSEG

LEVEL:	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	SPRITE,SETCOLSPR,DELSPR
	

INITSPRITES:
	CALL	DELSPR
	LD	BC,1*256 + PTRPAT
	LD	DE,ED.SPRITES
	JP	SPRITE

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	DISSPR,ENASPR,DISSCR,ENASCR,CLRVPAGE,LOCATE,PRINTF,SETPAGE

LEVELSCR:
	CALL	DISSPR
	CALL	DISSCR
	LD	A,LEVPAGE
	LD	(ACPAGE),A
	LD	(DPPAGE),A
	CALL	SETPAGE			;SET THE CORRECT PAGE
	LD	E,LEVPAGE
	CALL	CLRVPAGE		;CLEAN THE PAGE

	LD	DE,LISTCOORD
	CALL	LOCATE
	LD	B,NR_LEVELS
	LD	IX,LEVELS

L.LOOP:	PUSH	BC
	LD	L,(IX+LEVEL.YSIZ)
	PUSH	HL
	LD	L,(IX+LEVEL.XSIZ)
	PUSH	HL
	LD	L,(IX+LEVEL.NAME)
	LD	H,(IX+LEVEL.NAME+1)
	PUSH	HL
	LD	DE,L.FMT
	CALL	PRINTF
	LD	DE,LEVEL.SIZ
	ADD	IX,DE
	POP	BC
	DJNZ	L.LOOP

	CALL	ENASCR
	CALL	ENASPR
	RET

L.FMT:	9,9,"%s (%dX%d)",10,0


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


LEVELS:	DB	8,8
	DW	MOSQUE
	DB	4,4
	DW	ZAHRA
	DB	6,6
	DW	ZAHIRA


MOSQUE:	DB	"QURTUBA MOSQUE",0
ZAHRA:	DB	"MEDINA AL-ZAHRA",0
ZAHIRA:	DB	"MEDINA ZAHIRA",0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

ED.SPRITES:
	DB	080H,0C0H,0F0H,0F0H,0C0H,080H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H

