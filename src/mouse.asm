
	INCLUDE	BIOS.INC
	INCLUDE	SPRITE.INC

MOUSECOL	EQU	15

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	INITMOUSE
	EXTRN	SPRITE,SETCOLSPR

MOUSE1	EQU	12			;DEVICE ID FOR MOUSE 1
MOUSE2	EQU	16			;DEVICE ID FOR MOUSE 2

INITMOUSE:
	LD	C,MOUSEPAT
	LD	DE,PATTERN
	LD	B,4
	CALL	SPRITE

	LD	C,MOUSESPR
	LD	E,MOUSECOL
	LD	B,4
	CALL	SETCOLSPR

	LD	A,MOUSE1
	LD	(MPORT),A
	CALL	ISCONN			;IS IT CONNECTED?
	RET	NZ

	LD	A,MOUSE2
	LD	(MPORT),A
	CALL	ISCONN			;IS IT CONNECTED?
	RET	NZ

	XOR	A
	LD	(MPORT),A
	RET


PATTERN:
P.LUP:	DB	0F0H,0E0H,0F0H,0B8H,010H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H


P.RUP:	DB	078H,038H,078H,0E8H,040H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H

P.LDOWN:DB	010H,0B8H,0F0H,0E0H,0F0H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H


P.RDOWN:DB	040H,0E8H,078H,038H,078H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H
	DB	000H,000H,000H,000H,000H,000H,000H,000H

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = MOUSE PORT
;OUTPUT:ZF = 1 WHEN IT IS NOT CONNECTED

	CSEG

ISCONN:	LD	B,3
	LD	E,A

I.LOOP:	PUSH	DE			;READ THE MOUSE INCREMENT
	PUSH	BC			;IF YOU GET XINC=1 AT LEAST

	LD	A,E			;3 TIMES, WE CAN THINK IT IS
	PUSH	AF			;DISCONNECTED
	CALL	GTPAD
	POP	AF
	INC	A
	CALL	GTPAD
	POP	BC
	POP	DE

	CP	1
	RET	NZ
	EI
	HALT
	DJNZ	I.LOOP
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	MOUSEHOOK,MOUSEX,MOUSEY
	EXTRN	SEXPAND,PUTSPRITE

MOUSEHOOK:
	LD	A,(MPORT)
	OR	A
	RET	Z
	CALL	GTPAD			;CHECK MOUSE POSITION
	LD	HL,(MOUSEX)
	LD	H,0
	LD	DE,(SAVEX)
	CALL	SEXPAND			;CONVERT X INCREMENT TO 16 BITS
	ADD	HL,DE
	LD	DE,255
	CALL	ADJUST			;CHECK CORRECT POSITION
	LD	(MOUSEX),A

	LD	HL,(MOUSEY)
	LD	H,0
	LD	DE,(SAVEY)
	CALL	SEXPAND			;CONVERT Y INCREMENT TO 16 BITS
	ADD	HL,DE
	LD	DE,211
	CALL	ADJUST			;CHECK CORRECT POSITION
	LD	(MOUSEY),A

	LD	DE,(COORD)
	LD	BC,MOUSEPAT*256 + MOUSESPR
	JP	PUTSPRITE

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	HL = COORDENATE
;	DE = LIMIT
;OUTPUT:A = ADJUSTED COORDENATED

	CSEG

ADJUST:	LD	A,H
	BIT	7,H
	JR	NZ,A.NEG

	CALL	DCOMPR
	JP	NC,A.BIG
	LD	A,L
	RET

A.NEG:	XOR	A
	RET

A.BIG:	LD	A,E
	RET


	DSEG
MPORT:	DB	0

COORD:
MOUSEY:	DB	0
MOUSEX:	DB	0

