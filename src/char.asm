	INCLUDE	KBD.INC
	INCLUDE GEOMETRY.INC

NR_CHARS	EQU	8		;NUMBER OF CHARACTERS IN THE FULL GAME


;CHARACTER COORDENATES HAVE THE FORM (MAP,SCR,X,Y)

CHAR.MAP	EQU	0
CHAR.SCR	EQU	1
CHAR.X		EQU	2
CHAR.Y		EQU	3
CHAR.DIR	EQU	4
CHAR.DIRCNT	EQU	5
CHAR.PAT	EQU	6
CHAR.SIZ	EQU	7

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	HL = FUNCTION POINTER

	CSEG
	EXTRN	PTRCALL

FOREACH:LD	IX,BUFFER
	LD	B,NR_CHARS

F.LOOP:	PUSH	BC
	PUSH	HL
	CALL	PTRCALL
	POP	HL
	POP	BC

	LD	DE,CHAR.SIZ
	ADD	IX,DE
	DJNZ	F.LOOP
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	INITCHAR
	EXTRN	BZERO

INITCHAR:
	LD	HL,BUFFER
	LD	BC,CHAR.SIZ*NR_CHARS
	CALL	BZERO
	LD	HL,I.INIT
	CALL	FOREACH
	JP	NEWCHAR

I.INIT:	LD	(IX+CHAR.DIR),D.NODIR
	LD	(IX+CHAR.MAP),-1
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	NEWCHAR

NEWCHAR:LD	IX,BUFFER
	LD	B,NR_CHARS
	LD	A,-1

N.LOOP:	CP	(IX+CHAR.MAP)
	JR	Z,N.FOUND
	LD	DE,CHAR.SIZ
	ADD	IX,DE
	DJNZ	N.LOOP
	LD	HL,0
	RET				;TODO: HANDLE OVERRUN

N.FOUND:LD	(IX+CHAR.MAP),0		;REMOVE FROM FREE LIST
	LD	L,IXL
	LD	H,IXU
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	CHARACTERS

CHARACTERS:
	LD	IX,BUFFER
	;CONTINUE IN KBCONTROL

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	IX = POINTER TO THE CHARACTER

	CSEG
	EXTRN	GETCH,KEY2DIR,MOVEUC,WRLD2SCR,PUTMOB,EXIT

KBCONTROL:
	LD	A,(IX+CHAR.DIR)
	CP	D.NODIR
	JR	Z,K.GETCH
	DEC	(IX+CHAR.DIRCNT)
	JR	NZ,K.MOVE

K.GETCH:LD	A,D.NODIR
	LD	(IX+CHAR.DIR),A
	CALL	GETCH
	RET	Z
	CP	KB_ESC
	CALL	Z,EXIT
	CALL	KEY2DIR
	LD	(IX+CHAR.DIR),A
	RET	C
	LD	A,2
	LD	(IX+CHAR.DIRCNT),A

K.MOVE:	LD	A,(IX+CHAR.DIR)
	LD	D,(IX+CHAR.X)
	LD	E,(IX+CHAR.Y)
	CALL	MOVEUC
	LD	A,255
	CP	D
	RET	Z
	CP	E
	RET	Z
	LD	A,32			;TODO: CHANGE IT AND DON'T MIX
	CP	D			;SCREEN ISSUES WITH WORLD ISSUES
	RET	Z
	CP	E
	RET	Z
	LD	(IX+CHAR.X),D
	LD	(IX+CHAR.Y),E

	LD	BC,7814H
	CALL	WRLD2SCR
	LD	BC,0
	JP	PUTMOB

	DSEG
BUFFER:	DS	CHAR.SIZ * NR_CHARS
