

	INCLUDE	SHIKE2.INC
	INCLUDE	LEVEL.INC
	INCLUDE	DATA.INC

DOOR.MOB	EQU	0
DOOR.POINT	EQU	DOOR.MOB+SIZMOB
DOOR.STATE	EQU	DOOR.POINT+SIZPOINT
DOOR.TYPE	EQU	DOOR.STATE+1
DOOR.KEY	EQU	DOOR.TYPE+1
SIZDOOR		EQU	DOOR.KEY+1

DOOR.LEVEL	EQU	DOOR.POINT + POINT.LEVEL
DOOR.ROOM	EQU	DOOR.POINT + POINT.ROOM
DOOR.X		EQU	DOOR.POINT + POINT.X
DOOR.Y		EQU	DOOR.POINT + POINT.Y
DOOR.Z		EQU	DOOR.POINT + POINT.Z


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	DOORINIT
	EXTRN	CARTPAGE

DOORINIT:
	PUSH	IX
	LD	E,LEVELPAGE
	CALL	CARTPAGE
	CALL	GETDOOR			;DOOR INITIALIZATION DATA

	LD	B,NR_DOORS
	LD	IX,DOORBUF

D.LOOP:	PUSH	BC			;INITIALIZE ALL THE DOORS IN THE
	PUSH	HL			;GAME
	LD	E,L
	LD	D,H
	LD	BC,SIZPOINT
	ADD	HL,BC
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,(HL)
	CALL	DOOR
	POP	HL
	LD	DE,SIZDINFO
	ADD	HL,DE
	POP	BC
	DJNZ	D.LOOP

	POP	IX
	RET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	IX = POINTER TO THE DOOR
;	DE = POINTER TO INITIAL POINT
;	C = DOOR TYPE
;	B = DOOR KEY
;	A = INITIAL STATE


	CSEG

DOOR:	LD	(IX+DOOR.TYPE),C
	LD	(IX+DOOR.KEY),B
	LD	(IX+DOOR.STATE),A
	LD	C,IXL
	LD	B,IXU
	LD	HL,DOOR.POINT
	ADD	HL,BC
	EX	DE,HL
	LD	BC,SIZPOINT
	LDIR
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	SHOWDOORS
	EXTRN	GETRINFO,WRLD2SCR,PUTMOB

SHOWDOORS:
	PUSH	IX
	LD	B,NR_DOORS
	LD	IX,DOORBUF

S.LOOP:	PUSH	BC
	LD	E,(IX+DOOR.LEVEL)
	LD	D,(IX+DOOR.LEVEL+1)
	LD	C,(IX+DOOR.ROOM)
	LD	B,(IX+DOOR.ROOM+1)
	CALL	GETRINFO		;TAKE THE INFORMATION OF THE
	JR	Z,S.END			;ROOM DISPLAYED

	PUSH	HL
	POP	IY
	LD	E,(IY+RINFO.XR)
	LD	D,(IY+RINFO.XR+1)
	LD	C,(IY+RINFO.YR)
	LD	B,(IY+RINFO.YR+1)
	LD	L,(IX+DOOR.Y)
	LD	H,(IX+DOOR.X)
	LD	A,(IX+DOOR.Z)
	CALL	WRLD2SCR

	LD	B,(IY+DOOR.TYPE)
	LD	A,(IX+DOOR.Z)
	CALL	PUTMOB			;DRAW THE DOOR

S.END:	LD	DE,SIZDOOR
	ADD	IX,DE
	POP	BC
	DJNZ	S.LOOP
	POP	IX
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	DSEG
DOORBUF:DS	NR_DOORS*SIZDOOR

