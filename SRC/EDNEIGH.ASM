
	INCLUDE	BIOS.INC
	INCLUDE	SHIKE2.INC
	INCLUDE	LEVEL.INC
	INCLUDE	EVENT.INC
	INCLUDE	DATA.INC

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	PUBLIC	ED.NEIGH
	EXTRN	ADDGLISTEN,PUTLPAGE,EDINIT,LISTEN,VDPSYNC

ED.NEIGH:CALL	EDINIT
	LD	DE,R.PTR
	LD	BC,POSEVENT
	CALL	ADDGLISTEN

ED.LOOP:CALL	PUTLPAGE
	CALL	GETRDATA
	CALL	SHOWSCR
	CALL	VDPSYNC
	LD	DE,RECEIVERS
	CALL	LISTEN
	JR	NZ,ED.LOOP
	RET


RECEIVERS:
	DB	156,30,158,8
	DW	LEVELPAL
	DB	156,30,166,8
	DW	LEVELSET
R.PTR:	DS	64*6
	DB	0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	EDLEVEL,GETLEVEL

GETRDATA:
	LD	DE,(EDLEVEL)
	CALL	GETLEVEL
	RET	Z
	PUSH	HL
	POP	IY
	LD	A,(IY+LVL.PAL)
	LD	(PALETE),A
	LD	A,(IY+LVL.GFX)
	LD	(PATSET),A
	LD	(LPTR),IY
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	GLINES,PTRHL,PRINTF,LOCATE,EDLEVEL,GRID16,GETROOM
	EXTRN	GETACCS,MOFFSET,LMMM,VDPPAGE

SHOWSCR:CALL	GRID16
	CALL	DRAWRMATRIX

	LD	DE,0
	CALL	LOCATE
	LD	HL,(LPTR)
	PUSH	HL
	LD	DE,TITLE
	CALL	PRINTF

	LD	DE,0054H
	CALL	LOCATE

	LD	DE,(EDLEVEL)
	LD	BC,(EDROOM)
	LD	A,2
	CALL	GETROOM
	CALL	PTRHL
	PUSH	HL

	LD	DE,(EDLEVEL)
	LD	BC,(EDROOM)
	LD	A,1
	CALL	GETROOM
	CALL	PTRHL
	PUSH	HL

	LD	HL,(PATSET)
	LD	H,0
	PUSH	HL

	LD	DE,(EDLEVEL)
	LD	BC,(EDROOM)
	XOR	A
	CALL	GETROOM
	CALL	PTRHL
	PUSH	HL

	LD	HL,(PALETE)
	LD	H,0
	PUSH	HL

	LD	H,0
	LD	DE,(EDROOM)
	LD	L,E
	PUSH	HL
	LD	L,D
	PUSH	HL
	LD	DE,ROOMI
	CALL	PRINTF

	LD	C,15
	LD	DE,ROOMG
	CALL	GLINES

	; TODO: CLEAR THE ARROW AREA BEFORE DRAWING THE ARROWS

	LD	DE,(EDLEVEL)
	CALL	GETACCS
	LD	DE,(EDROOM)
	CALL	MOFFSET
	LD	A,(HL)
	PUSH	AF
	PUSH	AF
	PUSH	AF
	AND	1
	LD	A,DRIGHT
	CALL	NZ,ARROW

	POP	AF
	AND	2
	LD	A,DDOWN
	CALL	NZ,ARROW

	POP	AF
	AND	4
	LD	A,DUP
	CALL	NZ,ARROW

	POP	AF
	AND	8
	LD	A,DLEFT
	CALL	NZ,ARROW
	RET

ARROW:	RLCA
	RLCA
	RLCA
	RLCA
	LD	H,A
	LD	L,DIRY
	LD	D,190
	LD	E,178
	LD	A,FONTPAGE
	LD	(VDPPAGE),A
	LD	BC,16*256 + 16
	LD	A,LOGTIMP
	LD	(LOGOP),A
	JP	LMMM


;	       REP  X0  Y0    X1  Y1 IX0 IY0 IX1 IY1
ROOMG:	DB	4, 156,158,  186,158,  0,  8,  0,  8
	DB	2, 156,158,  156,182, 30,  0, 30,  0
	DB	0

TITLE:	DB	9,"NEIGHBORHOOD EDITOR: %s",0
ROOMI:	DB	9,"ROOM",9,"       %02dX%02d",9,9,"PALETE",9,"%d",10
	DB	9,"HEIGHT 0",9,"%04d",9,9,"SET",9,"%d",10
	DB	9,"HEIGHT 1",9,"%04d",9,9,"ACCESS",9,10
	DB	9,"HEIGHT 2",9,"%04d",0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	EDLEVEL,COLORGRID16,GETROOM

DRAWRMATRIX:
	LD	B,ROOMYSIZ
	LD	DE,0

S.LOOPY:PUSH	BC
	PUSH	DE
	LD	B,ROOMXSIZ

S.LOOPX:PUSH	BC
	PUSH	DE
	PUSH	DE
	LD	C,E
	LD	B,D
	LD	DE,(EDLEVEL)
	LD	A,0
	CALL	GETROOM
	POP	DE
	JR	Z,S.ENDX
	LD	A,(HL)
	INC	HL
	OR	(HL)
	CALL	NZ,COLORGRID16

S.ENDX:	POP	DE
	INC	D
	POP	BC
	DJNZ	S.LOOPX

	POP	DE
	INC	E
	POP	BC
	DJNZ	S.LOOPY
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT:	A = EVENT
;	DE = SCREEN POSITION

	CSEG
	EXTRN	EDROOM,ED.MAP,GRIDPOS,GETACCS,MOFFSET

POSEVENT:
	PUSH	AF
	CALL	GRIDPOS
	POP	AF
	CP	MS_BUTTON1
	JR	NZ,P.BUT2
	LD	(EDROOM),DE
	CALL	ED.MAP
	JP	EDINIT

P.BUT2:	CP	MS_BUTTON2
	JR	NZ,P.F2
	LD	(EDROOM),DE
	RET

P.F2:	CP	KB_F2
	LD	D,1
	JR	Z,P.ACCS
	CP	KB_F3
	LD	D,2
	JR	Z,P.ACCS
	CP	KB_F4
	LD	D,4
	JR	Z,P.ACCS
	CP	KB_F5
	RET	NZ
	LD	D,8

P.ACCS:	PUSH	DE
	LD	DE,(EDLEVEL)
	CALL	GETACCS
	LD	DE,(EDROOM)
	CALL	MOFFSET
	POP	AF
	XOR	(HL)
	LD	(HL),A
	RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CSEG
	EXTRN	SETPAL,GETPAL

LEVELPAL:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT PALETE NUMBER
	JR	NZ,P.DEC
	LD	A,(PALETE)
	CP	NR_PALETES-1
	RET	Z
	INC	A
	JR	P.RET

P.DEC:	CP	MS_BUTTON2
	RET	NZ
	LD	A,(PALETE)
	OR	A			;BUTTON 2 DECREMENT PALETE NUMBER
	RET	Z
	DEC	A
P.RET:	LD	IY,(LPTR)
	LD	(PALETE),A
	LD	(IY+LVL.PAL),A
	LD	E,A
	CALL	GETPAL
	JP	SETPAL

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;INPUT: A = EVENT

	CSEG
	EXTRN	LDPATSET

LEVELSET:
	CP	MS_BUTTON1		;BUTTON 1 INCREMENT SET NUMBER
	JR	NZ,S.DEC
	LD	A,(PATSET)
	CP	NR_PATSET-1
	RET	Z
	INC	A
	JR	S.RET

S.DEC:	CP	MS_BUTTON2
	RET	NZ
	LD	A,(PATSET)
	OR	A			;BUTTON 2 DECREMENT SET NUMBER
	RET	Z
	DEC	A
S.RET:	LD	(PATSET),A
	LD	IY,(LPTR)
	LD	(IY+LVL.GFX),A
	RET
	LD	E,A			;THERE IS A SET CHANGE, SO UPDATE
	JP	LDPATSET		;THE GRAPHICS

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	DSEG
LPTR:	DW	0
PALETE:	DB	0
PATSET:	DB	0

